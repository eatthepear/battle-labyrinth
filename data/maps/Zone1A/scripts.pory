mapscripts Zone1A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE1A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE1A_QUICK_CLAW)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone1A_EventScript_GiveFWG {
	lock
	faceplayer
	if (flag(FLAG_ZONE1A_RECEIVED_FWG)) {
		msgbox(format("Uh… It looks like I'm actually almost out of Pokémon to give, so I can't give you anymore. Sorry!"))
		release
		end
	}
	msgbox(format("Hello! You look like a new trainer.\pDo you know about Pokémon type matchups?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("Type matchups are one of the most important parts of Pokémon battles!\pThe most basic type matchup is Grass beats Water, Water beats Fire, and Fire beats Grass.\pIt can be very important to have a diversity of types on your team."), MSGBOX_DEFAULT)
	} else {
		msgbox(format("Oh! Then I'm sure you could see the benefits of having a diversity of types on your team."), MSGBOX_DEFAULT)
	}
	msgbox(format("I'm offering trainers a Grass, Fire, or Water type Pokémon to help supplement their teams. Interested?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(Zone1A_Text_FWG_ComeBack, MSGBOX_AUTOCLOSE)
		release
		end
	}
	message(format("Do you want a Grass, Fire, or Water type?"))
	dynmultichoice(0, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Grass", "Fire", "Water", "Exit")
	switch (var(VAR_RESULT)) {
		case 0:
			setvar(VAR_TEMP_4, 0)
		case 1:
			setvar(VAR_TEMP_4, 1)
		case 2:
			setvar(VAR_TEMP_4, 2)
		case 3:
		case MULTI_B_PRESSED:
			msgbox(Zone1A_Text_FWG_ComeBack, MSGBOX_AUTOCLOSE)
			release
			end
	}
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		if (flag(FLAG_SETTINGS_RANDOMIZER) == true) {
			setvar(VAR_TEMP_1, SPECIES_BULBASAUR) // gets randomized anyways
		} else {
			if (var(VAR_TEMP_4) == 0) {
				msgbox(format("On Nuzlocke mode, you will randomly receive one of the possible Pokémon: Pansage, Gossifleur, Sunkern, or Cherubi.\pIs this ok?"), MSGBOX_YESNO)
				if (var(VAR_RESULT) == NO) {
					msgbox(Zone1A_Text_FWG_ComeBack)
					release
					end
				}
				setvar(VAR_TEMP_3, 0)
				checkspeciesclause(SPECIES_PANSAGE)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_GOSSIFLEUR)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_SUNKERN)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_CHERUBI)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				if (var(VAR_TEMP_3) == 4) {
					msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."), MSGBOX_AUTOCLOSE)
					release
					end
				}
				setvar(VAR_TEMP_1, 0)
				while (var(VAR_TEMP_1) == 0) {
					random(4)
					switch (var(VAR_RESULT)) {
						case 0:
							checkspeciesclause(SPECIES_PANSAGE)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_PANSAGE)
						case 1:
							checkspeciesclause(SPECIES_GOSSIFLEUR)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_GOSSIFLEUR)
						case 2:
							checkspeciesclause(SPECIES_SUNKERN)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_SUNKERN)
						case 3:
							checkspeciesclause(SPECIES_CHERUBI)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_CHERUBI)
					}
				}
			} elif (var(VAR_TEMP_4) == 1) {
				msgbox(format("On Nuzlocke mode, you will randomly receive one of the possible Pokémon: Pansear, Vulpix, Slugma, or Litleo.\pIs this ok?"), MSGBOX_YESNO)
				if (var(VAR_RESULT) == NO) {
					msgbox(Zone1A_Text_FWG_ComeBack)
					release
					end
				}
				setvar(VAR_TEMP_3, 0)
				checkspeciesclause(SPECIES_PANSEAR)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_VULPIX)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_SLUGMA)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_LITLEO)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				if (var(VAR_TEMP_3) == 4) {
					msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."), MSGBOX_AUTOCLOSE)
					release
					end
				}
				setvar(VAR_TEMP_1, 0)
				while (var(VAR_TEMP_1) == 0) {
					random(4)
					switch (var(VAR_RESULT)) {
						case 0:
							checkspeciesclause(SPECIES_PANSEAR)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_PANSEAR)
						case 1:
							checkspeciesclause(SPECIES_VULPIX)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_VULPIX)
						case 2:
							checkspeciesclause(SPECIES_SLUGMA)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_SLUGMA)
						case 3:
							checkspeciesclause(SPECIES_LITLEO)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_LITLEO)
					}
				}
			} elif (var(VAR_TEMP_4) == 2) {
				msgbox(format("On Nuzlocke mode, you will randomly receive one of the possible Pokémon: Panpour, Barboach, Goldeen, or Finneon.\pIs this ok?"), MSGBOX_YESNO)
				if (var(VAR_RESULT) == NO) {
					msgbox(Zone1A_Text_FWG_ComeBack)
					release
					end
				}
				setvar(VAR_TEMP_3, 0)
				checkspeciesclause(SPECIES_PANPOUR)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_BARBOACH)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_GOLDEEN)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				checkspeciesclause(SPECIES_FINNEON)
				if (var(VAR_RESULT) == TRUE) {
					addvar(VAR_TEMP_3, 1)
				}
				if (var(VAR_TEMP_3) == 4) {
					msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."), MSGBOX_AUTOCLOSE)
					release
					end
				}
				setvar(VAR_TEMP_1, 0)
				while (var(VAR_TEMP_1) == 0) {
					random(4)
					switch (var(VAR_RESULT)) {
						case 0:
							checkspeciesclause(SPECIES_PANPOUR)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_PANPOUR)
						case 1:
							checkspeciesclause(SPECIES_BARBOACH)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_BARBOACH)
						case 2:
							checkspeciesclause(SPECIES_GOLDEEN)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_GOLDEEN)
						case 3:
							checkspeciesclause(SPECIES_FINNEON)
							if (var(VAR_RESULT) == TRUE) {
								continue
							}
							setvar(VAR_TEMP_1, SPECIES_FINNEON)
					}
				}
			}
		}
	} else {
		if (var(VAR_TEMP_4) == 0) {
			message(format("Choose which Pokémon you would like."))
			waitmessage
			dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Pansage", "Gossifleur", "Sunkern", "Cherubi", "Exit")
			switch (var(VAR_RESULT)) {
				case 0:
					setvar(VAR_TEMP_1, SPECIES_PANSAGE)
				case 1:
					setvar(VAR_TEMP_1, SPECIES_GOSSIFLEUR)
				case 2:
					setvar(VAR_TEMP_1, SPECIES_SUNKERN)
				case 3:
					setvar(VAR_TEMP_1, SPECIES_CHERUBI)
				case 4:
				case MULTI_B_PRESSED:
					msgbox(Zone1A_Text_FWG_ComeBack)
					release
					end
			}
		} elif (var(VAR_TEMP_4) == 1) {
			message(format("Choose which Pokémon you would like."))
			waitmessage
			dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Pansear", "Vulpix", "Slugma", "Litleo", "Exit")
			switch (var(VAR_RESULT)) {
				case 0:
					setvar(VAR_TEMP_1, SPECIES_PANSEAR)
				case 1:
					setvar(VAR_TEMP_1, SPECIES_VULPIX)
				case 2:
					setvar(VAR_TEMP_1, SPECIES_SLUGMA)
				case 3:
					setvar(VAR_TEMP_1, SPECIES_LITLEO)
				case 4:
				case MULTI_B_PRESSED:
					msgbox(Zone1A_Text_FWG_ComeBack)
					release
					end
			}
		} elif (var(VAR_TEMP_4) == 2) {
			message(format("Choose which Pokémon you would like."))
			waitmessage
			dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Panpour", "Barboach", "Goldeen", "Finneon", "Exit")
			switch (var(VAR_RESULT)) {
				case 0:
					setvar(VAR_TEMP_1, SPECIES_PANPOUR)
				case 1:
					setvar(VAR_TEMP_1, SPECIES_BARBOACH)
				case 2:
					setvar(VAR_TEMP_1, SPECIES_GOLDEEN)
				case 3:
					setvar(VAR_TEMP_1, SPECIES_FINNEON)
				case 4:
				case MULTI_B_PRESSED:
					msgbox(Zone1A_Text_FWG_ComeBack)
					release
					end
			}
		}
		bufferspeciesname(0, VAR_TEMP_1)
		msgbox(format("Are you sure you want {STR_VAR_1}?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			msgbox(Zone1A_Text_FWG_ComeBack)
			release
			end
		}
	}
	specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		msgbox(format("Your party is full. Choose a Pokémon to send to the PC."), MSGBOX_AUTOCLOSE)
		special(ChoosePartyMon)
		waitstate
		faceplayer
		if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
			msgbox(Zone1A_Text_FWG_ComeBack)
			release
			end
		}
		specialvar(VAR_TEMP_3, ScriptGetPartyMonSpecies)
		bufferspeciesname(2, VAR_TEMP_3)
		copyvar(VAR_TEMP_3, VAR_0x8004) // used in SendChosenMonToPC
		msgbox(format("Are you sure you want to send your {STR_VAR_3} to the PC?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			msgbox(Zone1A_Text_FWG_ComeBack)
			release
			end
		}
		special(SendChosenMonToPC)
		setvar(VAR_TEMP_2, 5) // gift mon appears always in last slot if party initially full
	}
	givemon(VAR_TEMP_1, 5, ITEM_NONE)
	setmonmetlocation(VAR_TEMP_2, 245)
	bufferspeciesname(0, VAR_TEMP_1)
	msgbox("Here's your {STR_VAR_1}.", MSGBOX_DEFAULT)
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	msgbox(format("Would you like to give a nickname to {STR_VAR_1}?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		call(Common_EventScript_GetGiftMonPartySlot)
		call(Common_EventScript_NameReceivedPartyMon)
	}
	faceplayer
	msgbox(format("Take care of your {STR_VAR_1}!"))
	setflag(FLAG_ZONE1A_RECEIVED_FWG)
	release
	end
}

text Zone1A_Text_FWG_ComeBack {
	format("Come back another time!")
}

movement Zone1A_Corey_WalkLeft {
	walk_left * 4
}

movement Zone1A_Corey_WalkRight {
	walk_right * 4
}

script Zone1A_EventScript_TriggerLeviathanBottom {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	applymovement(1, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(1, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(1, Zone1A_Corey_WalkLeft)
	waitmovement(0)
	msgbox(Zone1A_Text_Leviathan1Intro, MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		goto(Zone1A_EventScript_LeviathanBattle)
	}
	msgbox(Zone1A_Text_Leviathan1Wait, MSGBOX_AUTOCLOSE)
	applymovement(1, Common_Movement_WalkUp)
	waitmovement(0)
	applymovement(1, Zone1A_Corey_WalkRight)
	waitmovement(0)
	applymovement(1, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	releaseall
}

script Zone1A_EventScript_TriggerLeviathanMiddle {
	lockall
	applymovement(1, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(1, Zone1A_Corey_WalkLeft)
	waitmovement(0)
	msgbox(Zone1A_Text_Leviathan1Intro, MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		goto(Zone1A_EventScript_LeviathanBattle)
	}
	msgbox(Zone1A_Text_Leviathan1Wait, MSGBOX_AUTOCLOSE)
	applymovement(1, Zone1A_Corey_WalkRight)
	waitmovement(0)
	applymovement(1, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	releaseall
}

script Zone1A_EventScript_TriggerLeviathanTop {
	lockall
	applymovement(1, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(1, Common_Movement_WalkUp)
	waitmovement(0)
	applymovement(1, Zone1A_Corey_WalkLeft)
	waitmovement(0)
	msgbox(Zone1A_Text_Leviathan1Intro, MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		goto(Zone1A_EventScript_LeviathanBattle)
	}
	msgbox(Zone1A_Text_Leviathan1Wait, MSGBOX_AUTOCLOSE)
	applymovement(1, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(1, Zone1A_Corey_WalkRight)
	waitmovement(0)
	applymovement(1, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	releaseall
}

script Zone1A_EventScript_LeviathanBattle {
	fadeoutbgm(1)
	setvar(VAR_ZONE_1_STATE, 1)
	msgbox(Zone1A_Text_Leviathan1Intro2)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
		switch (var(VAR_STARTER_MON)) {
			case 0:
				trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_GRASS_STARTER_CHALLENGE, Zone1A_Text_Leviathan1Defeat)
			case 1:
				trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_FIRE_STARTER_CHALLENGE, Zone1A_Text_Leviathan1Defeat)
			case 2:
				trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_WATER_STARTER_CHALLENGE, Zone1A_Text_Leviathan1Defeat)
		}
	} else {
		switch (var(VAR_STARTER_MON)) {
			case 0:
				trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_GRASS_STARTER, Zone1A_Text_Leviathan1Defeat)
			case 1:
				trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_FIRE_STARTER, Zone1A_Text_Leviathan1Defeat)
			case 2:
				trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_WATER_STARTER, Zone1A_Text_Leviathan1Defeat)
		}
	}
	settrainerflag(TRAINER_PBL_COREY_LEVIATHAN_1_GRASS_STARTER)
	settrainerflag(TRAINER_PBL_COREY_LEVIATHAN_1_GRASS_STARTER_CHALLENGE)
	settrainerflag(TRAINER_PBL_COREY_LEVIATHAN_1_FIRE_STARTER)
	settrainerflag(TRAINER_PBL_COREY_LEVIATHAN_1_FIRE_STARTER_CHALLENGE)
	settrainerflag(TRAINER_PBL_COREY_LEVIATHAN_1_WATER_STARTER)
	settrainerflag(TRAINER_PBL_COREY_LEVIATHAN_1_WATER_STARTER_CHALLENGE)
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: I thought if I had a variety of types, I would be able to win.\pI guess there's more to battling than just having different Pokémon!\pAs a reward for beating me, take this Exp. Share! It's useful for training up Pokémon."), MSGBOX_DEFAULT)
	giveitem(ITEM_EXP_SHARE)
	msgbox(format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: Well, anyways, I've gotta run now.\pBye bye!"), MSGBOX_AUTOCLOSE)
	closemessage
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(1)
	setflag(FLAG_BEAT_LEVIATHAN_1)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	release
	end
}

text Zone1A_Text_Leviathan1Intro {
	format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: Hi! My name's Corey.\n"
		   "I'm the resident Leviathan around here.\p"
		   "If you want to go forward, you'll have to beat me!\p"
		   "So, are you ready for our battle?")
}

text Zone1A_Text_Leviathan1Wait {
	format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: Okay. You just let me know when you're ready.")
}

text Zone1A_Text_Leviathan1Intro2 {
	format("{COLOR BLUE}Corey{COLOR DARK_GRAY}: Aww, yeah!\n"
		   "This battle is gonna be awesome!")
}

text Zone1A_Text_Leviathan1Defeat {
	format("Drats! My Fire-Water-Grass core wasn't enough!")
}

script Zone1A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE1A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE1A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone1A_EventScript_QuickClaw {
	lock
	if (flag(FLAG_ITEM_ZONE1A_QUICK_CLAW)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE1A_QUICK_CLAW)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_QUICK_CLAW, 1)
	release
	end
}

raw `
Zone1A_EventScript_Nina::
	trainerbattle_single TRAINER_PBL_NINA, Zone1A_Text_NinaIntro, Zone1A_Text_NinaDefeat
	msgbox Zone1A_Text_NinaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_NinaIntro:
	.string "Hey, you! Let's battle!$"

Zone1A_Text_NinaDefeat:
	.string "Aww… Come on!$"

Zone1A_Text_NinaPostBattle:
	.string "I always lose…$"

Zone1A_EventScript_Mariam::
	trainerbattle_single TRAINER_PBL_MARIAM, Zone1A_Text_MariamIntro, Zone1A_Text_MariamDefeat
	msgbox Zone1A_Text_MariamPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_MariamIntro:
	.string "Be careful, I'm tough!$"

Zone1A_Text_MariamDefeat:
	.string "Sheesh!$"

Zone1A_Text_MariamPostBattle:
	.string "Maybe I need more Pokémon.$"

Zone1A_EventScript_Oscar::
	trainerbattle_single TRAINER_PBL_OSCAR, Zone1A_Text_OscarIntro, Zone1A_Text_OscarDefeat
	msgbox Zone1A_Text_OscarPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_OscarIntro:
	.string "Fear my might!$"

Zone1A_Text_OscarDefeat:
	.string "Beaten! Disgrace!$"

Zone1A_Text_OscarPostBattle:
	.string "I must reclaim my honor…$"
`
