mapscripts Zone1A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE1A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE1A_QUICK_CLAW)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone1A_EventScript_GiveFWG {
	lock
	faceplayer
	if (flag(FLAG_ZONE1A_RECEIVED_FWG)) {
		msgbox(format("Uh… It looks like I'm actually almost out of Pokémon to give, so I can't give you anymore. Sorry!"))
		release
		end
	}
	msgbox(format("Hello! You look like a new trainer.\pDo you know about Pokémon type matchups?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("Type matchups are one of the most important parts of Pokémon battles!\pThe most basic type matchup is Grass beats Water, Water beats Fire, and Fire beats Grass.\pIt can be very important to have a diversity of types on your team."))
	} else {
		msgbox(format("Oh! Then I'm sure you could see the benefits of having a diversity of types on your team."))
	}
	msgbox(format("I'm offering trainers a Grass, Fire, or Water type Pokémon to help supplement their teams. Interested?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(Zone1A_Text_FWG_ComeBack, MSGBOX_AUTOCLOSE)
		release
		end
	}
	message(format("Do you want a Grass, Fire, or Water type?"))
	dynmultichoice(0, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Grass", "Fire", "Water", "Exit")
	switch (var(VAR_RESULT)) {
		case 0:
			setvar(VAR_TEMP_A, SPECIES_PANSAGE)
			setvar(VAR_TEMP_B, SPECIES_GOSSIFLEUR)
			setvar(VAR_TEMP_C, SPECIES_SUNKERN)
			setvar(VAR_TEMP_D, SPECIES_CHERUBI)
			setvar(VAR_TEMP_E, SPECIES_FOMANTIS)
		case 1:
			setvar(VAR_TEMP_A, SPECIES_PANSEAR)
			setvar(VAR_TEMP_B, SPECIES_VULPIX)
			setvar(VAR_TEMP_C, SPECIES_SLUGMA)
			setvar(VAR_TEMP_D, SPECIES_SIZZLIPEDE)
			setvar(VAR_TEMP_E, SPECIES_PONYTA)
		case 2:
			setvar(VAR_TEMP_A, SPECIES_PANPOUR)
			setvar(VAR_TEMP_B, SPECIES_PSYDUCK)
			setvar(VAR_TEMP_C, SPECIES_GOLDEEN)
			setvar(VAR_TEMP_D, SPECIES_FINNEON)
			setvar(VAR_TEMP_E, SPECIES_KRABBY)
		case 3:
		case MULTI_B_PRESSED:
			msgbox(Zone1A_Text_FWG_ComeBack, MSGBOX_AUTOCLOSE)
			release
			end
	}
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		msgbox(gText_Nuzlocke_GiftPossible, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			if (var(VAR_TEMP_A) == SPECIES_PANSAGE) {
				msgbox(format("Pansage, Gossifleur, Sunkern, Cherubi, or Fomantis."))
			} elif (var(VAR_TEMP_A) == SPECIES_PANSEAR) {
				msgbox(format("Pansear, Vulpix, Slugma, Sizzlipede, or Ponyta."))
			} elif (var(VAR_TEMP_A) == SPECIES_PANPOUR) {
				msgbox(format("Panpour, Psyduck, Goldeen, Finneon, or Krabby."))
			}
		}
		msgbox(gText_Nuzlocke_ReadyReceive, MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			msgbox(Zone1A_Text_FWG_ComeBack)
			release
			end
		}
		setvar(VAR_TEMP_3, 0)
		if (checkspeciesclause(VAR_TEMP_A) == TRUE) {
			addvar(VAR_TEMP_3, 1)
		}
		if (checkspeciesclause(VAR_TEMP_B) == TRUE) {
			addvar(VAR_TEMP_3, 1)
		}
		if (checkspeciesclause(VAR_TEMP_C) == TRUE) {
			addvar(VAR_TEMP_3, 1)
		}
		if (checkspeciesclause(VAR_TEMP_D) == TRUE) {
			addvar(VAR_TEMP_3, 1)
		}
		if (checkspeciesclause(VAR_TEMP_E) == TRUE) {
			addvar(VAR_TEMP_3, 1)
		}
		if (var(VAR_TEMP_3) == 5) {
			msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."), MSGBOX_AUTOCLOSE)
			release
			end
		}
		setvar(VAR_TEMP_0, 0)
		while (var(VAR_TEMP_0) == 0) {
			random(5)
			switch (var(VAR_RESULT)) {
				case 0:
					if (checkspeciesclause(VAR_TEMP_A) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_A)
				case 1:
					if (checkspeciesclause(VAR_TEMP_B) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_B)
				case 2:
					if (checkspeciesclause(VAR_TEMP_C) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_C)
				case 3:
					if (checkspeciesclause(VAR_TEMP_D) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_D)
				case 4:
					if (checkspeciesclause(VAR_TEMP_E) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_E)
			}
		}
	} else {
		message(format("Choose which Pokémon you would like."))
		waitmessage
		bufferspeciesname(0, VAR_TEMP_A)
		dynmultipush("{STR_VAR_1}", 0)
		bufferspeciesname(0, VAR_TEMP_B)
		dynmultipush("{STR_VAR_1}", 1)
		bufferspeciesname(0, VAR_TEMP_C)
		dynmultipush("{STR_VAR_1}", 2)
		bufferspeciesname(0, VAR_TEMP_D)
		dynmultipush("{STR_VAR_1}", 3)
		bufferspeciesname(0, VAR_TEMP_E)
		dynmultipush("{STR_VAR_1}", 4)
		dynmultipush("Exit", 5)
		dynmultistack(0, 0, FALSE, 6, 0, 0, DYN_MULTICHOICE_CB_NONE)
		switch (var(VAR_RESULT)) {
			case 0:
				copyvar(VAR_TEMP_0, VAR_TEMP_A)
			case 1:
				copyvar(VAR_TEMP_0, VAR_TEMP_B)
			case 2:
				copyvar(VAR_TEMP_0, VAR_TEMP_C)
			case 3:
				copyvar(VAR_TEMP_0, VAR_TEMP_D)
			case 4:
				copyvar(VAR_TEMP_0, VAR_TEMP_E)
			case 5:
			case MULTI_B_PRESSED:
				msgbox(Zone1A_Text_FWG_ComeBack)
				release
				end
		}
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(format("Are you sure you want {STR_VAR_1}?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			msgbox(Zone1A_Text_FWG_ComeBack)
			release
			end
		}
	}
	bufferspeciesname(0, VAR_TEMP_0)
	msgbox("Here's your {STR_VAR_1}.")
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		msgbox(format("Your party is full. Would you like to send a party Pokémon to the PC and add {STR_VAR_1} to your party?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(EventScript_ChoosePartyMonToSend)
		}
	}
	givemon(VAR_TEMP_0, 5, metLocation=METLOC_GIVE_FWG)
	if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_GetGiftMonPartySlot)
			call(Common_EventScript_NameReceivedPartyMon)
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		copyvar(VAR_TEMP_TRANSFERRED_SPECIES, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_NameReceivedBoxMon)
		}
		call(Common_EventScript_TransferredToPC)
	}
	setflag(FLAG_ZONE1A_RECEIVED_FWG)
	msgbox(format("Take care of your new Pokémon!"))
	release
	end
}

text Zone1A_Text_FWG_ComeBack {
	format("Come back another time!")
}

movement Zone1A_Corey_WalkLeft {
	walk_left * 4
}

movement Zone1A_Corey_WalkRight {
	walk_right * 4
}

script Zone1A_EventScript_TriggerLeviathanBottom {
	lockall
	applymovement(LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	applymovement(LOCALID_COREY, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(LOCALID_COREY, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(LOCALID_COREY, Zone1A_Corey_WalkLeft)
	waitmovement(0)
	call(Zone1A_EventScript_LeviathanBattle)
	applymovement(LOCALID_COREY, Common_Movement_WalkUp)
	waitmovement(0)
	applymovement(LOCALID_COREY, Zone1A_Corey_WalkRight)
	waitmovement(0)
	applymovement(LOCALID_COREY, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	releaseall
}

script Zone1A_EventScript_TriggerLeviathanMiddle {
	lockall
	applymovement(LOCALID_COREY, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(LOCALID_COREY, Zone1A_Corey_WalkLeft)
	waitmovement(0)
	call(Zone1A_EventScript_LeviathanBattle)
	applymovement(LOCALID_COREY, Zone1A_Corey_WalkRight)
	waitmovement(0)
	applymovement(LOCALID_COREY, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	releaseall
}

script Zone1A_EventScript_TriggerLeviathanTop {
	lockall
	applymovement(LOCALID_COREY, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(LOCALID_COREY, Common_Movement_WalkUp)
	waitmovement(0)
	applymovement(LOCALID_COREY, Zone1A_Corey_WalkLeft)
	waitmovement(0)
	call(Zone1A_EventScript_LeviathanBattle)
	applymovement(LOCALID_COREY, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(LOCALID_COREY, Zone1A_Corey_WalkRight)
	waitmovement(0)
	applymovement(LOCALID_COREY, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	releaseall
}

script Zone1A_EventScript_LeviathanBattle {
	setspeaker("Corey")
	msgbox(Zone1A_Text_Leviathan1Intro, MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		setspeaker("Corey")
		msgbox(Zone1A_Text_Leviathan1Wait, MSGBOX_AUTOCLOSE)
		return
	}
	fadeoutbgm(1)
	setspeaker("Corey")
	msgbox(Zone1A_Text_Leviathan1Intro2)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	switch (var(VAR_STARTER_MON)) {
		case 0:
			trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_GRASS_STARTER, Zone1A_Text_Leviathan1Defeat)
		case 1:
			trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_FIRE_STARTER, Zone1A_Text_Leviathan1Defeat)
		case 2:
			trainerbattle_no_intro(TRAINER_PBL_COREY_LEVIATHAN_1_WATER_STARTER, Zone1A_Text_Leviathan1Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setspeaker("Corey")
	msgbox(format("I thought if I had a variety of types, I would be able to win.\pI guess there's more to battling than just having different Pokémon!\pAs a reward for beating me, take this TM!\pIt's useful for catching new Pokémon."))
	giveitem(ITEM_TM_FALSE_SWIPE)
	setspeaker("Corey")
	msgbox(format("Well, anyways, I've gotta run now.\pBye bye!"), MSGBOX_AUTOCLOSE)
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(LOCALID_COREY)
	setflag(FLAG_BEAT_LEVIATHAN_1)
	setvar(VAR_ZONE_1_STATE, 1)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	releaseall
	end
}

text Zone1A_Text_Leviathan1Intro {
	format("Hi! My name's Corey.\n"
		   "I'm the resident Leviathan around here.\p"
		   "If you want to go forward, you'll have to beat me!\p"
		   "So, are you ready for our battle?")
}

text Zone1A_Text_Leviathan1Wait {
	format("Okay. You just let me know when you're ready!")
}

text Zone1A_Text_Leviathan1Intro2 {
	format("Aww, yeah!\n"
		   "This battle is gonna be awesome!")
}

text Zone1A_Text_Leviathan1Defeat {
	format("Drats! My Fire-Water-Grass core wasn't enough!")
}

script Zone1A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE1A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE1A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_0, RandomItemChest)
	finditem(VAR_TEMP_0, VAR_TEMP_0)
	release
	end
}

script Zone1A_EventScript_QuickClaw {
	lock
	if (flag(FLAG_ITEM_ZONE1A_QUICK_CLAW)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE1A_QUICK_CLAW)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_QUICK_CLAW, 1)
	release
	end
}

raw `
Zone1A_EventScript_Nina::
	trainerbattle_single TRAINER_PBL_NINA, Zone1A_Text_NinaIntro, Zone1A_Text_NinaDefeat
	msgbox Zone1A_Text_NinaPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_NinaIntro:
	.string "Hey, you! Let's battle!$"

Zone1A_Text_NinaDefeat:
	.string "Aww… Come on!$"

Zone1A_Text_NinaPostBattle:
	.string "I always lose…$"

Zone1A_EventScript_Mariam::
	trainerbattle_single TRAINER_PBL_MARIAM, Zone1A_Text_MariamIntro, Zone1A_Text_MariamDefeat
	msgbox Zone1A_Text_MariamPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_MariamIntro:
	.string "Be careful, I'm tough!$"

Zone1A_Text_MariamDefeat:
	.string "Sheesh!$"

Zone1A_Text_MariamPostBattle:
	.string "Maybe I need more Pokémon.$"

Zone1A_EventScript_Oscar::
	trainerbattle_single TRAINER_PBL_OSCAR, Zone1A_Text_OscarIntro, Zone1A_Text_OscarDefeat
	msgbox Zone1A_Text_OscarPostBattle, MSGBOX_AUTOCLOSE
	end

Zone1A_Text_OscarIntro:
	.string "Fear my might!$"

Zone1A_Text_OscarDefeat:
	.string "Beaten! Disgrace!$"

Zone1A_Text_OscarPostBattle:
	.string "I must reclaim my honor…$"
`
