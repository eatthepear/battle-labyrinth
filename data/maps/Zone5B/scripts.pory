mapscripts Zone5B_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_TEMP_HIDE_FOLLOWER)
		if (var(VAR_DIFFICULTY) == DIFFICULTY_BRUTAL) {
			setvar(VAR_BATTLE_STARTING_STATUS, STARTING_STATUS_PSYCHIC_TERRAIN)
		}
    }
}

script Zone5B_EventScript_Liza {
    lock
	if (flag(FLAG_BEAT_LEVIATHAN_5)) {
		msgbox(format("{COLOR BLUE}Liza{COLOR DARK_GRAY}: You will become even stronger! I've battled you, so I know."))
		release
		end
	}
	msgbox(format("{COLOR BLUE}Liza{COLOR DARK_GRAY}: Hehehe… Were you surprised?\pThis is my own Pokémon Gym, my very own, without my brother anywhere near!\pWhile it would be nice to be around him sometimes…\pNever mind. You've brought yourself, and you're looking for a challenge.\pI hope to meet that expectation!\pBring it!"))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_LIZA_LEVIATHAN_5, format("What? I lost?"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Liza{COLOR DARK_GRAY}: It can't be helped. You've won…\pCongratulations. As a reward, take this!"))
	giveitem(ITEM_TM_LIGHT_SCREEN)
	msgbox(format("{COLOR BLUE}Liza{COLOR DARK_GRAY}: That's it!\pFufufu…\pJust kidding, here's the other one as well. Us Gym Leaders give out multiple rewards!"))
	giveitem(ITEM_TM_REFLECT)
	msgbox(format("{COLOR BLUE}Liza{COLOR DARK_GRAY}: You're doing great, challenger!\pKeep up the good work.\pLeft leads to the exit, while right returns you to the Gym entrance."))
    setflag(FLAG_BEAT_LEVIATHAN_5)
	release
	end
}

script Zone5B_EventScript_GymStatue {
    lockall
	msgbox(format("Liza's Pokémon Gym"))
	releaseall
	end
}

script Zone5B_EventScript_GymGuide {
    lock
    faceplayer
    msgbox(format("Welcome to Liza's Pokémon Gym!\pYou'll find some challenging puzzles up ahead to test your mind.\pThe Gym Leader, Liza, is without her brother currently, so be prepared for a single battle!"))
	if (var(VAR_DIFFICULTY) == DIFFICULTY_BRUTAL) {
		msgbox(format("Also, beware! The Gym is under the effect of Psychic Terrain, meaning all grounded Pokémon deal 30% more with Psychic moves and are immune to priority moves."))
	}
    release
    end
}

script Zone5B_EventScript_YellowFloorSwitch {
    setvar(VAR_TEMP_0, 0)
    goto(Zone5B_EventScript_FloorSwitch)
}

script Zone5B_EventScript_BlueFloorSwitch {
    setvar(VAR_TEMP_0, 1)
    goto(Zone5B_EventScript_FloorSwitch)
}

script Zone5B_EventScript_GreenFloorSwitch {
    setvar(VAR_TEMP_0, 2)
    goto(Zone5B_EventScript_FloorSwitch)
}

script Zone5B_EventScript_PurpleFloorSwitch {
    setvar(VAR_TEMP_0, 3)
    goto(Zone5B_EventScript_FloorSwitch)
}

script Zone5B_EventScript_RedFloorSwitch {
    setvar(VAR_TEMP_0, 4)
    goto(Zone5B_EventScript_FloorSwitch)
}

script Zone5B_EventScript_FloorSwitch {
	playse(SE_SWITCH)
	waitse
	playse(SE_REPEL)
	initrotatingtilepuzzle(FALSE)
	moverotatingtileobjects(VAR_TEMP_0)
	waitmovement(0)
	turnrotatingtileobjects
	waitmovement(0)
	freerotatingtilepuzzle
    end
}

script Zone5B_EventScript_WarpToEntrance {
	lockall
	warpmossdeepgym(MAP_ZONE5B, 0, 31)
	waitstate
	releaseall
	end
}

script Zone5B_EventScript_WarpToPriorWarp {
	lockall
	warpmossdeepgym(MAP_ZONE5B, 4, 11)
	waitstate
	releaseall
	end
}

script Zone5B_EventScript_Sylvia {
	trainerbattle_single(TRAINER_PBL_SYLVIA, format("Psychic Pokémon are the most powerful!"), format("I stand corrected!"))
	msgbox(format("You are the most powerful!\pWell, aside from our Gym Leader!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone5B_EventScript_Theresa {
	trainerbattle_single(TRAINER_PBL_THERESA, format("What a strange place to find myself in…"), format("I find myself feeling dizzy for some reaon!"))
	msgbox(format("Surely the spinning isn't messing with my head, right?"), MSGBOX_AUTOCLOSE)
	end
}

script Zone5B_EventScript_Anthony {
	trainerbattle_single(TRAINER_PBL_ANTHONY, format("Have you used a Rare Candy yet?\pYou might want to, if you want any hope of beating the Gym Leader!"), format("That's if you can figure out the puzzle first!"))
	msgbox(format("Rare Candies are used best to evolve your Pokémon or learn a new move."), MSGBOX_AUTOCLOSE)
	end
}
