mapscripts ZoneSanctuary_PokemonCenter_2F_MapScripts {
    
}

script ZoneSanctuary_PokemonCenter_2F_EventScript_NameRater {
	lock
	faceplayer
	msgbox(ZoneSanctuary_PokemonCenter_2F_Text_PleasedToRateMonNickname, MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        msgbox(ZoneSanctuary_PokemonCenter_2F_Text_CritiqueWhichMonNickname)
        special(ChoosePartyMon)
        waitstate
        if (var(VAR_0x8004) != PARTY_NOTHING_CHOSEN) {
            specialvar(VAR_RESULT, ScriptGetPartyMonSpecies)
            if (var(VAR_RESULT) == SPECIES_EGG) {
                msgbox(ZoneSanctuary_PokemonCenter_2F_Text_ThatIsMerelyAnEgg)
                release
                end
            }
            special(BufferMonNickname)
            msgbox(ZoneSanctuary_PokemonCenter_2F_Text_FineNameSuggestBetterOne, MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                msgbox(ZoneSanctuary_PokemonCenter_2F_Text_WhatShallNewNameBe, MSGBOX_DEFAULT)
                call(Common_EventScript_NameReceivedPartyMon)
                special(BufferMonNickname)
                msgbox(ZoneSanctuary_PokemonCenter_2F_Text_MonShallBeKnownAsName, MSGBOX_DEFAULT)
                release
                end
            }
        }
    }
	msgbox(ZoneSanctuary_PokemonCenter_2F_Text_DoVisitAgain, MSGBOX_DEFAULT)
	release
	end
}

raw `
ZoneSanctuary_PokemonCenter_2F_Text_PleasedToRateMonNickname:
	.string "Hi, hi! I'm the Name Rater!\n"
	.string "I'm the fortune-teller of names!\p"
	.string "I shall be pleased to rate your\n"
	.string "Pokémon's nickname.$"

ZoneSanctuary_PokemonCenter_2F_Text_CritiqueWhichMonNickname:
	.string "Which Pokémon's nickname should\n"
	.string "I critique?$"

ZoneSanctuary_PokemonCenter_2F_Text_FineNameSuggestBetterOne:
	.string "Hmmm… {STR_VAR_1}, is it? That is\n"
	.string "quite a fine name you bestowed.\p"
	.string "But! What say you, if I were to\n"
	.string "suggest a slightly better name?$"

ZoneSanctuary_PokemonCenter_2F_Text_WhatShallNewNameBe:
	.string "Ah, good. Then, what shall the new\n"
	.string "nickname be?$"

ZoneSanctuary_PokemonCenter_2F_Text_MonShallBeKnownAsName:
	.string "Done! From now on, this Pokémon\n"
	.string "shall be known as {STR_VAR_1}!\p"
	.string "This is a magnificent nickname!\n"
	.string "It is impeccably beyond reproach!\p"
	.string "You'll do well to cherish your\n"
	.string "{STR_VAR_1} now and beyond.$"

ZoneSanctuary_PokemonCenter_2F_Text_DoVisitAgain:
	.string "I see.\n"
	.string "Do come visit again.$"

ZoneSanctuary_PokemonCenter_2F_Text_ThatIsMerelyAnEgg:
	.string "Now, now.\n"
	.string "That is merely an Egg!$"
`
