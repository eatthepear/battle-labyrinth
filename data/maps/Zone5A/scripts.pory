mapscripts Zone5A_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_DIFFICULTY) == DIFFICULTY_BRUTAL) {
			setvar(VAR_BATTLE_STARTING_STATUS, 0)
		}
    }
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE5A_MIRACLE_SEED)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE5A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE5A_TM_THIEF)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_TM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_TM_CHEST)
		}
	}
}

script Zone5A_EventScript_BuyPikaClone {
	lock
	faceplayer
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true && flag(FLAG_ZONE5A_NUZLOCKE_BOUGHT_PIKACLONE) == true) {
		msgbox(format("Sorry! I'm all out of Pikachu clones for sale right now."))
		release
		end
	}
	showmoneybox(18, 0, 0)
	msgbox(format("You seem like the type that would like Pikachu!\pI have Pikachu and plenty of similar Pokémon available, each for only ¥500!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		hidemoneybox
		msgbox(gText_BuyPikaClone_ComeBack)
		release
		end
	}
	checkmoney(500, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	setvar(VAR_TEMP_1, SPECIES_PACHIRISU)
	setvar(VAR_TEMP_2, SPECIES_DEDENNE)
	setvar(VAR_TEMP_3, SPECIES_MORPEKO)
	setvar(VAR_TEMP_4, SPECIES_EMOLGA)
	setvar(VAR_TEMP_5, SPECIES_PLUSLE)
	setvar(VAR_TEMP_6, SPECIES_MINUN)
	setvar(VAR_TEMP_7, SPECIES_PIKACHU)
	setvar(VAR_TEMP_8, SPECIES_PAWMI)
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		msgbox(gText_Nuzlocke_GiftPossible, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			msgbox(format("Pachirisu, Dedenne, Morpeko, Emolga, Plusle, Minun, Pikachu, Pawmi."))
		}
		setvar(VAR_TEMP_A, 0)
		if (checkspeciesclause(VAR_TEMP_1) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_2) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_3) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_4) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_5) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_6) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_7) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_8) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (var(VAR_TEMP_A) == 8) {
			msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."))
			release
			end
		}
		msgbox(gText_Nuzlocke_ReadyReceive, MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(gText_BuyPikaClone_ComeBack)
			release
			end
		}
		setvar(VAR_TEMP_0, 0)
		while (var(VAR_TEMP_0) == 0) {
			random(8)
			switch (var(VAR_RESULT)) {
				case 0:
					if (checkspeciesclause(VAR_TEMP_1) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_1)
				case 1:
					if (checkspeciesclause(VAR_TEMP_2) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_2)
				case 2:
					if (checkspeciesclause(VAR_TEMP_3) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_3)
				case 3:
					if (checkspeciesclause(VAR_TEMP_4) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_4)
				case 4:
					if (checkspeciesclause(VAR_TEMP_5) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_5)
				case 5:
					if (checkspeciesclause(VAR_TEMP_6) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_6)
				case 6:
					if (checkspeciesclause(VAR_TEMP_7) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_7)
				case 7:
					if (checkspeciesclause(VAR_TEMP_8) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_8)
			}
		}
	} else {
		message(format("Choose which Electric-type you would like."))
		bufferspeciesname(0, VAR_TEMP_1)
		dynmultipush("{STR_VAR_1}", 0)
		bufferspeciesname(0, VAR_TEMP_2)
		dynmultipush("{STR_VAR_1}", 1)
		bufferspeciesname(0, VAR_TEMP_3)
		dynmultipush("{STR_VAR_1}", 2)
		bufferspeciesname(0, VAR_TEMP_4)
		dynmultipush("{STR_VAR_1}", 3)
		bufferspeciesname(0, VAR_TEMP_5)
		dynmultipush("{STR_VAR_1}", 4)
		bufferspeciesname(0, VAR_TEMP_6)
		dynmultipush("{STR_VAR_1}", 5)
		bufferspeciesname(0, VAR_TEMP_7)
		dynmultipush("{STR_VAR_1}", 6)
		bufferspeciesname(0, VAR_TEMP_8)
		dynmultipush("{STR_VAR_1}", 7)
		dynmultipush("Exit", 8)
		dynmultistack(0, 0, FALSE, 6, 0, 0, DYN_MULTICHOICE_CB_NONE)
		switch (var(VAR_RESULT)) {
			case 0:
				copyvar(VAR_TEMP_0, VAR_TEMP_1)
			case 1:
				copyvar(VAR_TEMP_0, VAR_TEMP_2)
			case 2:
				copyvar(VAR_TEMP_0, VAR_TEMP_3)
			case 3:
				copyvar(VAR_TEMP_0, VAR_TEMP_4)
			case 4:
				copyvar(VAR_TEMP_0, VAR_TEMP_5)
			case 5:
				copyvar(VAR_TEMP_0, VAR_TEMP_6)
			case 6:
				copyvar(VAR_TEMP_0, VAR_TEMP_7)
			case 7:
				copyvar(VAR_TEMP_0, VAR_TEMP_8)
			case 8:
			case MULTI_B_PRESSED:
				closemessage
				hidemoneybox
				msgbox(gText_BuyPikaClone_ComeBack)
				release
				end
		}
	}
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		msgbox(format("Just to confirm…\pYou're sure you want to buy a Pokémon for ¥500?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(gText_BuyPikaClone_ComeBack)
			release
			end
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(format("Just to confirm…\pYou're sure you want to buy {STR_VAR_1} for ¥500?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(gText_BuyPikaClone_ComeBack)
			release
			end
		}
	}
	removemoney(500)
	updatemoneybox
	msgbox(format("I appreciate it!"))
	hidemoneybox
	bufferspeciesname(0, VAR_TEMP_0)
	msgbox("Here's your {STR_VAR_1}.")
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		msgbox(format("Your party is full. Would you like to send a party Pokémon to the PC and add {STR_VAR_1} to your party?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(EventScript_ChoosePartyMonToSend)
		}
	}
	givemon(VAR_TEMP_0, 10, metLocation=METLOC_BOUGHT_PIKA_CLONE)
	if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_GetGiftMonPartySlot)
			call(Common_EventScript_NameReceivedPartyMon)
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		copyvar(VAR_TEMP_TRANSFERRED_SPECIES, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_NameReceivedBoxMon)
		}
		call(Common_EventScript_TransferredToPC)
	}
	setflag(FLAG_ZONE5A_NUZLOCKE_BOUGHT_PIKACLONE)
	msgbox(format("Take good care of your new Electric-type, small mammal-like Pokémon!"))
	release
	end
}

text gText_BuyPikaClone_ComeBack {
	format("Alrighty, well, you just come on back if you change your mind.")
}

script Zone5A_EventScript_NeverMissTutor {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	msgbox(format("Hey there! Ever been stuck because the opposing Pokémon is too hard to hit? Well, I specialize in teaching certain moves that always hit, no matter how evasive the foe is!"))
	message(format("What move should I teach?\nThey will cost you ¥400."))
	waitmessage
	dynmultipush("Swift", MOVE_SWIFT)
	dynmultipush("Aerial Ace", MOVE_AERIAL_ACE)
	dynmultipush("Magical Leaf", MOVE_MAGICAL_LEAF)
	dynmultipush("Shock Wave", MOVE_SHOCK_WAVE)
	dynmultipush("Exit", 1)
	dynmultistack(21, 0, FALSE, 5, 0, 0, DYN_MULTICHOICE_CB_NONE)
	switch (var(VAR_RESULT)) {
		case MOVE_SWIFT:
			setvar(VAR_TEMP_0, MOVE_SWIFT)
			setvar(VAR_0x8005, MOVE_SWIFT)
		case MOVE_AERIAL_ACE:
			setvar(VAR_TEMP_0, MOVE_AERIAL_ACE)
			setvar(VAR_0x8005, MOVE_AERIAL_ACE)
		case MOVE_MAGICAL_LEAF:
			setvar(VAR_TEMP_0, MOVE_MAGICAL_LEAF)
			setvar(VAR_0x8005, MOVE_MAGICAL_LEAF)
		case MOVE_SHOCK_WAVE:
			setvar(VAR_TEMP_0, MOVE_SHOCK_WAVE)
			setvar(VAR_0x8005, MOVE_SHOCK_WAVE)
		case 1:
		case MULTI_B_PRESSED:
			hidemoneybox
			msgbox(Zone5A_Text_NeverMissTutor_Evasion)
			release
			end
	}
	checkmoney(400, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	buffermovename(0, VAR_TEMP_0)
	msgbox("Which Pokémon should learn\n{STR_VAR_1}?")
	special(ChooseMonForMoveTutor)
	waitstate
	lock
	faceplayer
	if (var(VAR_RESULT) == 0) {
		msgbox(Zone5A_Text_NeverMissTutor_Evasion)
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("There! It's done. Use that move well!"))
	removemoney(400)
	updatemoneybox
	msgbox(format("{PLAYER} paid ¥400."))
	hidemoneybox
	msgbox(Zone5A_Text_NeverMissTutor_Evasion)
	release
	end
}

text Zone5A_Text_NeverMissTutor_Evasion {
	format("Evasion is really irritating to deal with, isn't it?")
}

script Zone5A_EventScript_MiracleSeed {
	lock
	if (flag(FLAG_ITEM_ZONE5A_MIRACLE_SEED)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE5A_MIRACLE_SEED)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_MIRACLE_SEED)
	release
	end
}

script Zone5A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE5A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE5A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone5A_EventScript_TMThief {
	lock
	if (flag(FLAG_ITEM_ZONE5A_TM_THIEF)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_TM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE5A_TM_THIEF)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM_THIEF)
	release
	end
}

script Zone5A_EventScript_Caitlyn {
	trainerbattle_single(TRAINER_PBL_CAITLYN, format("This is such a nice spot to picnic! Shoo! Find your own spot!"), format("I guess you can join me…"))
	msgbox(format("Why's this the best spot to picnic?\nThe air is really nice, that's why!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone5A_EventScript_Rebecca {
	trainerbattle_single(TRAINER_PBL_REBECCA, format("Have you seen the Gym up ahead?"), format("I wish I hadn't!"))
	msgbox(format("Now I'm nervous for a Gym challenge I don't even plan to do!"), MSGBOX_AUTOCLOSE)
	end
}

// script Zone5A_EventScript_Clinton {
// 	trainerbattle_single(TRAINER_PBL_CLINTON, format("Perhaps if I bribed the Gym Leader, I would be able to get a badge!"), format("Unfortunately, I tried, and no dice!"))
// 	msgbox(format("She requested a single battle instead.\pMaybe her brother isn't here or something?"), MSGBOX_AUTOCLOSE)
// 	end
// }

script Zone5A_EventScript_Bill {
	trainerbattle_single(TRAINER_PBL_BILL_OPTIONAL_2, format("This Rare Candy here can be used to increase your Pokémon's level over the level cap.\pYou won't get it easily!"), format("Use the Rare Candy carefully! Maybe to evolve your starter or something…"))
	msgbox(format("I wish I was better at battling. My girlfriend is always telling me how to improve, but it's hard when I don't have much patience…"), MSGBOX_AUTOCLOSE)
	end
}
