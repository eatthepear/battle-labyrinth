mapscripts Zone10A_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ZONE_10_STATE, 0: Zone10A_EventScript_KimonoGirl_Intro
	]
    MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_ZONE_10_STATE) < 5) {
			setobjectxyperm(LOCALID_JASMINE, 47, 28)
			setobjectxyperm(LOCALID_STEELIX, 48, 28)
		}
    }
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE10A_TM_SCORCHING_SANDS)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_TM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_TM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE10A_SOFT_SAND)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE10A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE10A_BLACK_GLASSES)) {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone10A_EventScript_KimonoGirl_Intro {
	lockall
	playbgm(MUS_HG_KIMONO_GIRL, 0)
	applymovement(LOCALID_KIMONO_GIRL_10A, Common_Movement_FaceRight)
	waitmovement(0)
	applymovement(LOCALID_KIMONO_GIRL_10A, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	setspeaker("Kimono Girl")
	msgbox(format("Oh, poor Kimono Girl, buffeted by the sandstorm.\pWon't you help show me the way to a shelter?"), MSGBOX_AUTOCLOSE)
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	applymovement(LOCALID_KIMONO_GIRL_10A, Common_Movement_WalkRight3)
	waitmovement(0)
	applymovement(LOCALID_KIMONO_GIRL_10A, Common_Movement_FaceUp)
	waitmovement(0)
	setspeaker("Kimono Girl")
	msgbox(Zone10A_Text_KimonoGirl, MSGBOX_AUTOCLOSE)
	setfollowernpc(LOCALID_KIMONO_GIRL_10A, FOLLOWER_NPC_FLAG_ALL)
	applymovement(LOCALID_KIMONO_GIRL_10A, Common_Movement_FaceLeft)
	waitmovement(0)
	setvar(VAR_ZONE_10_STATE, 1)
	fadedefaultbgm
	releaseall
}

text Zone10A_Text_KimonoGirl {
	format("It seems fate has smiled upon me today, meeting you.\pI do hope we find our way to a shelter soon…")
}

script Zone10A_EventScript_KimonoGirl {
	lock
	faceplayer
	setspeaker("Kimono Girl")
	msgbox(Zone10A_Text_KimonoGirl, MSGBOX_AUTOCLOSE)
	release
	end
}

script Zone10A_EventScript_KimonoGirl_Exit {
	lockall
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Common_Movement_FaceRight)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	playbgm(MUS_HG_KIMONO_GIRL, 0)
	facefollowernpc
	setspeaker("Kimono Girl")
	msgbox(format("Oh, look! The stairs to a shelter!\pThe poor Kimono Girl has been led to a shelter by a kind trainer!"))
	msgbox(format("GIVEITEM CHUNK"), MSGBOX_AUTOCLOSE)
	giveitem(ITEM_SACRED_ASH)
	setspeaker("Kimono Girl")
	msgbox(format("Goodbye."), MSGBOX_AUTOCLOSE)
	getobjectcurrentxy(OBJ_EVENT_ID_NPC_FOLLOWER, destX=VAR_TEMP_0, destY=VAR_TEMP_1)
	if (var(VAR_TEMP_1) == 33) {
		applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Zone10A_Movement_KimonoGirl_Exit_Top)
		waitmovement(0)
	} elif (var(VAR_TEMP_1) == 34) {
		applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Zone10A_Movement_KimonoGirl_Exit_TopMiddle)
		waitmovement(0)
	} elif (var(VAR_TEMP_1) == 35) {
		applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Zone10A_Movement_KimonoGirl_Exit_BottomMiddle)
		waitmovement(0)
	} elif (var(VAR_TEMP_1) == 36) {
		applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Zone10A_Movement_KimonoGirl_Exit_Bottom)
		waitmovement(0)
	}
	fadescreenswapbuffers(FADE_TO_BLACK)
	destroyfollowernpc
	fadescreenswapbuffers(FADE_FROM_BLACK)
	setvar(VAR_ZONE_10_STATE, 2)
	fadedefaultbgm
	releaseall
}

movement Zone10A_Movement_KimonoGirl_Exit_Top {
	walk_left
	walk_down * 2
	walk_right
}

movement Zone10A_Movement_KimonoGirl_Exit_TopMiddle {
	walk_down
	walk_right
}

movement Zone10A_Movement_KimonoGirl_Exit_BottomMiddle {
	walk_up
	walk_right * 2
}

movement Zone10A_Movement_KimonoGirl_Exit_Bottom {
	walk_left
	walk_up * 2
	walk_right
}

script Zone10A_EventScript_Jasmine {
	lock
	faceplayer
	if (flag(FLAG_BEAT_LEVIATHAN_10)) {
		setspeaker("Jasmine")
		msgbox(format("This desert may be harsh, but it's a great place to train Pokémon. Um… yep."))
		release
		end
	}
	if (var(VAR_ZONE_10_STATE) < 4) {
		if (var(VAR_ZONE_10_STATE) == 2) {
			setspeaker("Jasmine")
			msgbox(format("Um… hi, trainer.\pIf you want to enter this rest house, you have to prove your…\lum…\ldiligence."))
			setvar(VAR_ZONE_10_STATE, 3)
		}
		setspeaker("Jasmine")
		msgbox(format("In the desert, there are 5 hidden Blue Shards.\pFind them, and… return them to me."), MSGBOX_AUTOCLOSE)
		if ((flag(FLAG_HIDDEN_ITEM_ZONE10A_BLUE_SHARD_1) == false) ||
		(flag(FLAG_HIDDEN_ITEM_ZONE10A_BLUE_SHARD_2) == false) ||
		(flag(FLAG_HIDDEN_ITEM_ZONE10A_BLUE_SHARD_3) == false) ||
		(flag(FLAG_HIDDEN_ITEM_ZONE10A_BLUE_SHARD_4) == false) ||
		(flag(FLAG_HIDDEN_ITEM_ZONE10A_BLUE_SHARD_5) == false)) {
			release
			end
		}
		setspeaker("Jasmine")
		msgbox(format("Oh, great! You found all 5 Blue Shards. Then a final test remains: a battle.\p…\pPlease give me a moment to prepare myself."), MSGBOX_AUTOCLOSE)
		applymovement(LOCALID_PLAYER, Common_Movement_WalkDown2)
		waitmovement(0)
		setvar(VAR_ZONE_10_STATE, 4)
		release
		end
	}
	playbgm(MUS_HG_GYM, FALSE)
	setspeaker("Jasmine")
	msgbox(format("Allow me to introduce myself.\pMy name is Jasmine.\pI use the… clang!\nSteel type!\pSteel Pokémon are very tough and really strong!\pAnd they're especially good in a sandstorm!\pUm… I'm not lying."))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_JASMINE_LEVIATHAN_10, format("Well done…"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setspeaker("Jasmine")
	msgbox(format("You are a better Trainer than me, in both skill and kindness.\pI'd like you to have this."), MSGBOX_AUTOCLOSE)
	giveitem(ITEM_TM_IRON_TAIL)
	setspeaker("Jasmine")
	msgbox(format("Iron Tail is a strong, powerful Steel-type move.\pYou'll find that its accuracy has improved over time, and is not too shabby either.\pI'd also like you to have this."), MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EVIOLITE)
	setspeaker("Jasmine")
	msgbox(format("The Eviolite can help keep your weaker, unevolved Pokémon safe."), MSGBOX_AUTOCLOSE)
	delay(32)
	setspeaker("Jasmine")
	msgbox(format("Um… anyways, I'll get out of your way for now.\pGood luck on the rest of your journey, and keep training!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_STEELIX, Common_Movement_WalkRight)
	waitmovement(0)
	applymovement(LOCALID_STEELIX, Common_Movement_FaceDown)
	waitmovement(0)
	applymovement(LOCALID_JASMINE, Common_Movement_WalkRight)
	waitmovement(0)
	applymovement(LOCALID_JASMINE, Common_Movement_FaceDown)
	waitmovement(0)
	setflag(FLAG_BEAT_LEVIATHAN_10)
	setvar(VAR_ZONE_10_STATE, 5)
	release
	end
}

script Zone10A_EventScript_TMScorchingSands {
	lock
	if (flag(FLAG_ITEM_ZONE10A_TM_SCORCHING_SANDS)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_TM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE10A_TM_SCORCHING_SANDS)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM_SCORCHING_SANDS, 1)
	release
	end
}

script Zone10A_EventScript_SoftSand {
	lock
	if (flag(FLAG_ITEM_ZONE10A_SOFT_SAND)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE10A_SOFT_SAND)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SOFT_SAND, 1)
	release
	end
}

script Zone10A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE10A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE10A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone10A_EventScript_BlackGlasses {
	lock
	if (flag(FLAG_ITEM_ZONE10A_BLACK_GLASSES)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE10A_BLACK_GLASSES)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_BLACK_GLASSES, 1)
	release
	end
}

script Zone10A_EventScript_ParasolLady {
	lock
	faceplayer
	if (flag(FLAG_ZONE10A_RECEIVED_SAFETY_GOGGLES) == false) {
		msgbox(format("Hey there! This sandstorm is fierce for those unprepared!\pIt's not safe to be out in the sandstorm for long periods of time, so take this!"), MSGBOX_DEFAULT)
		giveitem(ITEM_SAFETY_GOGGLES)
		setflag(FLAG_ZONE10A_RECEIVED_SAFETY_GOGGLES)
	}
	msgbox(format("Remember kids, safety first!"))
	release
	end
}

script Zone10A_EventScript_Alana {
	trainerbattle_single(TRAINER_PBL_ALANA, format("Pfft!"), format("Sorry, I didn't mean to be rude!"))
	msgbox(format("Some sand flew into my mouth so I had to get it out!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone10A_EventScript_Dewey {
	trainerbattle_single(TRAINER_PBL_DEWEY, format("I came prepared for the wrath of the desert!"), format("But I was not prepared for you!"))
	msgbox(format("Preparations are important, be it for hiking or battling!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone10A_EventScript_Elias {
	trainerbattle_single(TRAINER_PBL_ELIAS, format("Tell me, have you faced the power of Dragons?"), format("No! My Dragons!"))
	msgbox(format("I must train them further!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone10A_EventScript_Pearlie {
	trainerbattle_single(TRAINER_PBL_PEARLIE, format("Backpacking together is safer!"), format("At least I have my Pokémon!"))
	msgbox(format("We really like backpacking together."), MSGBOX_AUTOCLOSE)
	end
}

script Zone10A_EventScript_Conrad {
	trainerbattle_single(TRAINER_PBL_CONRAD, format("Sand keeps getting in my lunch, so I'll take out my frustration on you!"), format("Ugh! Now there's sand everywhere!"))
	msgbox(format("I don't like sand.\n It's coarse and rough and irritating and it gets everywhere."), MSGBOX_AUTOCLOSE)
	end
}
