mapscripts Zone9A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE9A_BIG_ROOT)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE9A_GEMS)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE9A_TM_PLUCK)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_TM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_TM_CHEST)
		}
	}
}

script Zone9A_EventScript_Man {
	lock
	faceplayer
	msgbox(format("Hey there, champ!\pIn our modest village, there is a family of strong Trainers.\pWhy don't you go spar with them?"))
	release
	end
}

script Zone9A_EventScript_Ari {
	lock
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 17) {
		applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		applymovement(LOCALID_PLAYER, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 19) {
		applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		applymovement(LOCALID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
		applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
		waitmovement(0)
	}
	faceplayer
	setspeaker("Ari")
	msgbox(format("Howzit goin?\pOh, you're a traveler! You might be strong enough…\pI bet you can't defeat the five members of my family in a row!\pWhat do you say to my little wager?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		setspeaker("Ari")
		msgbox(format("Ah, so you're a chicken. Well, if you change your mind, I'll be here."))
		release
		end
	} else {
		setspeaker("Ari")
		msgbox(format("Alright, well, best of luck!"), MSGBOX_AUTOCLOSE)
		setflag(FLAG_FORCE_BATTLE_ANIM_ON)
		trainerbattle_no_intro(TRAINER_PBL_ARI, format("What!? How!?\nYou're tougher than I thought!"))
		applymovement(1, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		setspeaker("Ari")
		msgbox(format("Hey, everyone!\nI've found a pretty strong Trainer!"), MSGBOX_AUTOCLOSE)
		applymovement(1, Zone9A_Movement_WinstrateEnterHouse)
		waitmovement(0)
		removeobject(1)
		applymovement(LOCALID_PLAYER, Zone9A_Movement_WaitForNextWinstrate)
		waitmovement(0)
		addobject(2)
		applymovement(2, Common_Movement_WalkDown)
		waitmovement(0)
		setspeaker("Bertha")
		msgbox(format("You beat my brother? Ugh. Such a brat.\pLet's just get this over with."), MSGBOX_AUTOCLOSE)
		trainerbattle_no_intro(TRAINER_PBL_BERTHA, format("Sigh… I thought this would happen…"))
		applymovement(2, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		setspeaker("Bertha")
		msgbox(format("Mom! It's your turn!"), MSGBOX_AUTOCLOSE)
		applymovement(2, Zone9A_Movement_WinstrateEnterHouse)
		waitmovement(0)
		removeobject(2)
		applymovement(LOCALID_PLAYER, Zone9A_Movement_WaitForNextWinstrate)
		waitmovement(0)
		addobject(3)
		applymovement(3, Common_Movement_WalkDown)
		waitmovement(0)
		setspeaker("Cassandra")
		msgbox(format("Oh! I am so excited!\pFinally, a challenger who got past both of my children!\pIt's Momma's time to shine!"), MSGBOX_AUTOCLOSE)
		trainerbattle_no_intro(TRAINER_PBL_CASSANDRA, format("It was you who shined instead!"))
		applymovement(3, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		setspeaker("Cassandra")
		msgbox(format("It's all you, baby!"), MSGBOX_AUTOCLOSE)
		applymovement(3, Zone9A_Movement_WinstrateEnterHouse)
		waitmovement(0)
		removeobject(3)
		applymovement(LOCALID_PLAYER, Zone9A_Movement_WaitForNextWinstrate)
		waitmovement(0)
		addobject(4)
		applymovement(4, Common_Movement_WalkDown)
		waitmovement(0)
		setspeaker("Dominic")
		msgbox(format("Guess what time it is? It's time to take out the trash!"), MSGBOX_AUTOCLOSE)
		trainerbattle_no_intro(TRAINER_PBL_DOMINIC, format("Sorry, sorry! I didn't realize you were so strong!"))
		setspeaker("Dominic")
		msgbox(format("Kids!\nWake your grandma up!"), MSGBOX_AUTOCLOSE)
		applymovement(4, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		applymovement(4, Zone9A_Movement_WinstrateEnterHouse)
		waitmovement(0)
		removeobject(4)
		applymovement(LOCALID_PLAYER, Zone9A_Movement_WaitForNextWinstrate)
		waitmovement(0)
		addobject(5)
		applymovement(5, Common_Movement_WalkDown)
		waitmovement(0)
		if (var(VAR_DIFFICULTY) < DIFFICULTY_BRUTAL) {
			setspeaker("Erica")
			msgbox(format("You've defeated the rest of my family.\pAs an added challenge, would you feel up to facing me right now? If you win, I'll give you a {COLOR RED}Rare Candy{COLOR DARK_GRAY}."), MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES) {
				trainerbattle_no_intro(TRAINER_PBL_ERICA, format("Very impressive."))
				setspeaker("Erica")
				msgbox(format("As promised."))
				giveitem(ITEM_RARE_CANDY)
			} else {
				setspeaker("Erica")
				msgbox(format("Very well then."))
			}
		} else {
			setspeaker("Erica")
			msgbox(format("You've defeated the rest of my family.\pNow, you must face your final challenge.\pIf you defeat me, I'll give you a {COLOR RED}Rare Candy{COLOR DARK_GRAY}!"))
			trainerbattle_no_intro(TRAINER_PBL_ERICA, format("Very impressive."))
			setspeaker("Erica")
			msgbox(format("As promised."))
			giveitem(ITEM_RARE_CANDY)
		}
		setspeaker("Erica")
		msgbox(format("Finally, here is a little reward for completing my family's trial."))
		giveitem(ITEM_TM_RETALIATE)
		closemessage
		applymovement(5, Common_Movement_WalkInPlaceFasterUp)
		waitmovement(0)
		applymovement(5, Zone9A_Movement_WinstrateEnterHouse)
		waitmovement(0)
		removeobject(5)
		clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
		setflag(FLAG_HIDE_ZONE9A_ARI_WINSTRATE)
		release
		end
	}
}

movement Zone9A_Movement_WinstrateEnterHouse {
	walk_in_place_faster_up
	walk_up
}

movement Zone9A_Movement_WaitForNextWinstrate {
	delay_16 * 3
}

script Zone9A_EventScript_BigRoot {
	lock
	if (flag(FLAG_ITEM_ZONE9A_BIG_ROOT)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE9A_BIG_ROOT)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_BIG_ROOT, 1)
	release
	end
}

script Zone9A_EventScript_Gems {
	lock
	if (flag(FLAG_ITEM_ZONE9A_GEMS)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE9A_GEMS)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_POISON_GEM)
	finditem(ITEM_STEEL_GEM)
	finditem(ITEM_DARK_GEM)
	release
	end
}

script Zone9A_EventScript_TMPluck {
	lock
	if (flag(FLAG_ITEM_ZONE9A_TM_PLUCK)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_TM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE9A_TM_PLUCK)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM_PLUCK, 1)
	release
	end
}

script Zone9A_EventScript_Cooper {
	trainerbattle_single(TRAINER_PBL_COOPER, format("Bet you didn't expect me!"), format("I didn't expect you, either!"))
	msgbox(format("I just pop out occasionally in case someone is there."), MSGBOX_AUTOCLOSE)
	end
}

script Zone9A_EventScript_Carmine {
	trainerbattle_single(TRAINER_PBL_CARMINE, format("It's hot in the jungle!"), format("I think the humidity is the worst part!"))
	msgbox(format("I hope there's air conditioning in the village."), MSGBOX_AUTOCLOSE)
	end
}

script Zone9A_EventScript_Luis {
	trainerbattle_single(TRAINER_PBL_LUIS, format("Whew! I am tired. If I beat you in a Pokémon battle, do you think you could watch the little ones for a while?"), format("At least now they're out of energy!"))
	msgbox(format("Finally, I can take a short break!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone9A_EventScript_Demi {
	trainerbattle_single(TRAINER_PBL_DEMI, format("Ghosts haunt this village…"), format("I do my best to keep them at bay…"))
	msgbox(format("Ghosts are everywhere…"), MSGBOX_AUTOCLOSE)
	end
}

script Zone9A_EventScript_Kimberly {
	trainerbattle_single(TRAINER_PBL_KIMBERLY, format("Raising Pokémon is hard work!"), format("Am I working hard, or hardly working?"))
	msgbox(format("It doesn't feel like work when it's what I love!"), MSGBOX_AUTOCLOSE)
	end
}
