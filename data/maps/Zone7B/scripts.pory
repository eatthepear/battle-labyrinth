mapscripts Zone7B_MapScripts {}

script Zone7B_EventScript_LeviathanBattle {
	lock
	faceplayer
	if (flag(FLAG_BEAT_LEVIATHAN_7) == true) {
		setspeaker("Betty")
		msgbox(Zone7B_Text_LeviathanPost)
		release
		end
	}
	setspeaker("Betty")
	msgbox(format("Ah, another traveler. I grow tired of sitting around nibbling on Berries.\pCare to entertain a lady like me with a battle?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		setspeaker("Betty")
		msgbox(format("Well, I will be here, if you change your mind."))
		release
		end
	}
	fadeoutbgm(1)
	setspeaker("Betty")
	msgbox(format("I will show you how to behave in the prim and proper way!"), MSGBOX_DEFAULT)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_BETTY_LEVIATHAN_7, format("It seems I was the one who ended up being humbled…"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setspeaker("Betty")
	msgbox(format("Your battling skills are certainly up to par. Excellent form.\pI'd like to give you a few of these Berries.\pVery, very expensive, or so my butler tells me."), MSGBOX_AUTOCLOSE)
	giveitem(ITEM_SITRUS_BERRY, 5)
	setflag(FLAG_BEAT_LEVIATHAN_7)
	setspeaker("Betty")
	msgbox(Zone7B_Text_LeviathanPost)
	release
	end
}

text Zone7B_Text_LeviathanPost {
	format("I've been staying here at my friend's house while our new mansion gets renovated.\pIsn't that exciting?\pThough, I've been hearing that there's some people dressed in space suits messing around in the caves.\pSounds rather frightening, wouldn't you say?")
}

raw `
Zone7B_EventScript_Lady::
	lock
	faceplayer
	goto_if_set FLAG_ZONE7B_HEAL_USED, Zone7B_EventScript_AlreadyHealed
	msgbox Zone7B_Text_WantToHeal, MSGBOX_YESNO
	compare VAR_RESULT, YES
	goto_if_eq Zone7B_EventScript_Heal
	msgbox Zone7B_Text_ComeBack, MSGBOX_AUTOCLOSE
	release
	end

Zone7B_EventScript_AlreadyHealed:
	msgbox Zone7B_Text_GoodLuck, MSGBOX_AUTOCLOSE
	release
	end

Zone7B_EventScript_Heal:
	msgbox Zone7B_Text_RestWell, MSGBOX_AUTOCLOSE
	fadescreen FADE_TO_BLACK
	playfanfare MUS_HEAL
	waitfanfare
	special HealPlayerParty
	fadescreen FADE_FROM_BLACK
	setflag FLAG_ZONE7B_HEAL_USED
	goto Zone7B_EventScript_AlreadyHealed
	end

Zone7B_Text_WantToHeal:
	.string "Greetings, child.\n"
	.string "To ease your journey, you may rest\l"
	.string "at my humble home.\p"
	.string "I only have enough medicine to\n"
	.string "heal your Pokémon once though.\p"
	.string "Would you like to rest, child?$"

Zone7B_Text_RestWell:
	.string "Rest well, child.$"

Zone7B_Text_GoodLuck:
	.string "Best of luck on your journey, child.$"

Zone7B_Text_ComeBack:
	.string "Come back when you need to rest, child.$"
`
