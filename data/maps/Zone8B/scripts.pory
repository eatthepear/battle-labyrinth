mapscripts Zone8B_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE8B_HARD_STONE)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}


script Zone8B_EventScript_BuyTyrogue {
	lock
	faceplayer
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true && flag(FLAG_ZONE8B_NUZLOCKE_BOUGHT_FIGHTING) == true) {
		msgbox(format("BUSY NOW! BUSY TRAINING!"))
		release
		end
	}
	showmoneybox(18, 0, 0)
	msgbox(format("I'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pErr… Oh!\pYou look like a capable Trainer!\pAre you interested in raising a Fighting-type Pokémon?\pI won't part with one without ¥1000!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		hidemoneybox
		msgbox(Zone8B_Text_BackToTraining)
		release
		end
	}
	checkmoney(1000, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	setvar(VAR_TEMP_1, SPECIES_TYROGUE)
	setvar(VAR_TEMP_2, SPECIES_MAKUHITA)
	setvar(VAR_TEMP_3, SPECIES_TIMBURR)
	setvar(VAR_TEMP_4, SPECIES_MACHOP)
	setvar(VAR_TEMP_5, SPECIES_THROH)
	setvar(VAR_TEMP_6, SPECIES_SAWK)
	setvar(VAR_TEMP_7, SPECIES_FLAMIGO)
	setvar(VAR_TEMP_8, SPECIES_PASSIMIAN)
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		msgbox(gText_Nuzlocke_GiftPossible, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			msgbox(format("Tyrogue, Makuhita, Timburr, Machop, Throh, Sawk, Flamigo, Passimian."))
		}
		setvar(VAR_TEMP_A, 0)
		if (checkspeciesclause(VAR_TEMP_1) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_2) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_3) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_4) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_5) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_6) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_7) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (checkspeciesclause(VAR_TEMP_8) == TRUE) {
			addvar(VAR_TEMP_A, 1)
		}
		if (var(VAR_TEMP_A) == 8) {
			msgbox(format("Unfortunately, you have already obtained a Pokémon in each of these evolution lines."))
			release
			end
		}
		msgbox(gText_Nuzlocke_ReadyReceive, MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(Zone8B_Text_BackToTraining)
			release
			end
		}
		setvar(VAR_TEMP_0, 0)
		while (var(VAR_TEMP_0) == 0) {
			random(8)
			switch (var(VAR_RESULT)) {
				case 0:
					if (checkspeciesclause(VAR_TEMP_1) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_1)
				case 1:
					if (checkspeciesclause(VAR_TEMP_2) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_2)
				case 2:
					if (checkspeciesclause(VAR_TEMP_3) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_3)
				case 3:
					if (checkspeciesclause(VAR_TEMP_4) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_4)
				case 4:
					if (checkspeciesclause(VAR_TEMP_5) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_5)
				case 5:
					if (checkspeciesclause(VAR_TEMP_6) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_6)
				case 6:
					if (checkspeciesclause(VAR_TEMP_7) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_7)
				case 7:
					if (checkspeciesclause(VAR_TEMP_8) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_8)
			}
		}
	} else {
		message(format("CHOOSE YOUR FIGHTER!"))
		bufferspeciesname(0, VAR_TEMP_1)
		dynmultipush("{STR_VAR_1}", 0)
		bufferspeciesname(0, VAR_TEMP_2)
		dynmultipush("{STR_VAR_1}", 1)
		bufferspeciesname(0, VAR_TEMP_3)
		dynmultipush("{STR_VAR_1}", 2)
		bufferspeciesname(0, VAR_TEMP_4)
		dynmultipush("{STR_VAR_1}", 3)
		bufferspeciesname(0, VAR_TEMP_5)
		dynmultipush("{STR_VAR_1}", 4)
		bufferspeciesname(0, VAR_TEMP_6)
		dynmultipush("{STR_VAR_1}", 5)
		bufferspeciesname(0, VAR_TEMP_7)
		dynmultipush("{STR_VAR_1}", 6)
		bufferspeciesname(0, VAR_TEMP_8)
		dynmultipush("{STR_VAR_1}", 7)
		dynmultipush("Exit", 8)
		dynmultistack(0, 0, FALSE, 6, 0, 0, DYN_MULTICHOICE_CB_NONE)
		switch (var(VAR_RESULT)) {
			case 0:
				copyvar(VAR_TEMP_0, VAR_TEMP_1)
			case 1:
				copyvar(VAR_TEMP_0, VAR_TEMP_2)
			case 2:
				copyvar(VAR_TEMP_0, VAR_TEMP_3)
			case 3:
				copyvar(VAR_TEMP_0, VAR_TEMP_4)
			case 4:
				copyvar(VAR_TEMP_0, VAR_TEMP_5)
			case 5:
				copyvar(VAR_TEMP_0, VAR_TEMP_6)
			case 6:
				copyvar(VAR_TEMP_0, VAR_TEMP_7)
			case 7:
				copyvar(VAR_TEMP_0, VAR_TEMP_8)
			case 8:
			case MULTI_B_PRESSED:
				closemessage
				hidemoneybox
				msgbox(Zone8B_Text_BackToTraining)
				release
				end
		}
	}
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		msgbox(format("Just to confirm…\pYou're sure you want to buy a Pokémon for ¥1000?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(Zone8B_Text_BackToTraining)
			release
			end
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(format("Just to confirm…\pYou're sure you want to buy {STR_VAR_1} for ¥1000?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(Zone8B_Text_BackToTraining)
			release
			end
		}
	}
	removemoney(1000)
	updatemoneybox
	msgbox(format("GOOD PURCHASE!"))
	hidemoneybox
	bufferspeciesname(0, VAR_TEMP_0)
	msgbox("Here's your {STR_VAR_1}.")
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		msgbox(format("Your party is full. Would you like to send a party Pokémon to the PC and add {STR_VAR_1} to your party?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(EventScript_ChoosePartyMonToSend)
		}
	}
	givemon(VAR_TEMP_0, 18, metLocation=METLOC_ZONE8B_GIFT)
	if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_GetGiftMonPartySlot)
			call(Common_EventScript_NameReceivedPartyMon)
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		copyvar(VAR_TEMP_TRANSFERRED_SPECIES, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_NameReceivedBoxMon)
		}
		call(Common_EventScript_TransferredToPC)
	}
	faceplayer
	setflag(FLAG_ZONE8B_NUZLOCKE_BOUGHT_FIGHTING)
	msgbox(Zone8B_Text_BackToTraining)
	release
	end
}

text Zone8B_Text_BackToTraining {
	format("ALRIGHT! BACK TO TRAINING!")
}

script Zone8B_EventScript_GalacticGrunt {
	lock
	if (var(VAR_ZONE_8_STATE) != 0) {
		msgbox(Zone8B_Text_GalacticGrunt)
		release
		end
	}
	msgbox(format("I can do this!\pI'm a strong, independent man!\pI can turn to the Commander and say, I quit!\pShe doesn't scare me one bit!"), MSGBOX_AUTOCLOSE)
	delay(32)
	msgbox(format("Sigh, who am I kidding. She'd probably mop the floor with me…"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_GRUNT_8B, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	delay(16)
	faceplayer
	msgbox(format("You… You'll tell the Commander for me? Thank you so much!\pOh, but in order to get to her, you need the password: {COLOR BLUE}Dazzling{COLOR DARK_GRAY}!\pTell that to the guards, and they should let you through."))
	setvar(VAR_ZONE_8_STATE, 1)
	msgbox(Zone8B_Text_GalacticGrunt)
	release
	end
}

text Zone8B_Text_GalacticGrunt {
	format("Now, I'm free! I can finally get rid of this silly uniform.\pIt's so uncomfortable!")
}

script Zone8B_EventScript_HardStone {
	lock
	if (flag(FLAG_ITEM_ZONE8B_HARD_STONE)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE8B_HARD_STONE)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_HARD_STONE, 1)
	release
	end
}
