mapscripts Zone8D_MapScripts {}

script Zone8D_EventScript_LeviathanBattle {
	lockall
	applymovement(2, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 3) {
		applymovement(2, Common_Movement_WalkLeft2)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 4) {
		applymovement(2, Common_Movement_WalkLeft)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 6) {
		applymovement(2, Common_Movement_WalkRight)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 7) {
		applymovement(2, Common_Movement_WalkRight2)
		waitmovement(0)
	}
	applymovement(2, Common_Movement_WalkDown2)
	waitmovement(0)
	fadeoutbgm(1)
	setvar(VAR_ZONE_1_STATE, 1)
	msgbox(Zone8D_Text_Leviathan8Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
		trainerbattle_no_intro(TRAINER_PBL_ALISTAIR_LEVIATHAN_8_CHALLENGE, Zone8D_Text_Leviathan8Defeat)
	} else {
		trainerbattle_no_intro(TRAINER_PBL_ALISTAIR_LEVIATHAN_8, Zone8D_Text_Leviathan8Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Alistair{COLOR DARK_GRAY}: Ugh. I can't believe I lost this one.\pI guess I owe you a little something for winning."), MSGBOX_DEFAULT)
	giveitem(ITEM_TM13)
	msgbox(format("{COLOR BLUE}Alistair{COLOR DARK_GRAY}: It's a tough world out there. Watch your step."))
	closemessage
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(2)
	setvar(VAR_ZONE_8_STATE, 1)
	setflag(FLAG_BEAT_LEVIATHAN_8)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	release
	end
}

text Zone8D_Text_Leviathan8Intro {
	format("{COLOR BLUE}Alistair{COLOR DARK_GRAY}: Well, well, well. You're almost out, but you have another challenge to face: me.\pReady or not, here I come.")
}

text Zone8D_Text_Leviathan8Defeat {
	format("Beaten by a chump like you!")
}
