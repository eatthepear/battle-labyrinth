mapscripts Zone4E_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE4E_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE4E_POISON_BARB)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone4E_EventScript_KimonoGirl_Exit {
	lockall
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Common_Movement_FaceUp)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	playbgm(MUS_HG_KIMONO_GIRL, 0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("Oh, look! The exit!\pThe poor Kimono Girl has been led to the exit by a kind trainer!\pPlease take this as thanks."))
	giveitem(ITEM_SACRED_ASH)
	msgbox(format("Well, I shall be off.\pIt seems two await you up ahead."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PLAYER, Zone4E_Movement_Player_UnblockKimonoGirl)
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Zone4E_Movement_KimonoGirl_Exit)
	waitmovement(0)
	destroyfollowernpc
	setvar(VAR_ZONE_4_STATE, 2)
	fadedefaultbgm
	releaseall
}

movement Zone4E_Movement_Player_UnblockKimonoGirl {
	walk_down
	walk_right
	face_left
	delay_16 * 4
	face_up
	delay_16 * 6
	face_left
}

movement Zone4E_Movement_KimonoGirl_Exit {
	walk_left
	walk_right
	walk_up * 2
	walk_right * 4
	walk_up * 2
	walk_left * 4
	walk_up
	walk_left * 3
	walk_up
}

script Zone4E_EventScript_LeviathanBattle {
	lockall
	applymovement(LOCALID_BELLA, Common_Movement_WalkDown2)
	applymovement(LOCALID_ELLA, Common_Movement_WalkDown3)
	waitmovement(0)
	applymovement(LOCALID_ELLA, Common_Movement_FaceRight)
	waitmovement(0)
	fadeoutbgm(1)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	delay(16)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(16)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(16)
	msgbox(Zone4E_Text_Leviathan4Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_ELLA_AND_BELLA_LEVIATHAN_4, format("We lost!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Ella{COLOR DARK_GRAY}: Wargh! Not only are we lost in the wood…\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: We could've won that battle, you misunderstood!\p{COLOR BLUE}Ella{COLOR DARK_GRAY}: I guess our teamwork could be improved…\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Yeah… Oh right, here's a thing for those approved!"), MSGBOX_DEFAULT)
	giveitem(ITEM_TM_SNARL)
	msgbox(format("{COLOR BLUE}Ella{COLOR DARK_GRAY}: This TM can help in Double Battles!\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Shush, you're annoying them with your prattles!\p{COLOR BLUE}Ella{COLOR DARK_GRAY}: That rhyme barely worked!\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Oh, don't be irked!"))
	closemessage
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(LOCALID_ELLA)
	removeobject(LOCALID_BELLA)
	setflag(FLAG_BEAT_LEVIATHAN_4)
	setvar(VAR_ZONE_4_STATE, 3)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	releaseall
	end
}

text Zone4E_Text_Leviathan4Intro {
	format("{COLOR BLUE}Ella{COLOR DARK_GRAY}: Watch out!\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Don't fear!\p{COLOR BLUE}Ella{COLOR DARK_GRAY}: The dynamic duo of Ella & Bella…\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Will always persevere!\p{COLOR BLUE}Ella & Bella{COLOR DARK_GRAY}: It's time to battle it out, victory is near!")
}

script Zone4E_EventScript_StruggleBugTutor {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	if (flag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG) == TRUE) {
		msgbox(format("Hey! You made it to the end. I bet it was a struggle getting here, right?\pYou know what's not a struggle? The move Struggle Bug!\pI can teach Struggle Bug to one of your Pokémon for the low price of ¥200. How about it?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Okay, come back again."))
			release
			end
		}
	} else {
		msgbox(format("Hey! You made it to the end. I bet it was a struggle getting here, right?\pYou know what's not a struggle? The move Struggle Bug!\pI can teach Struggle Bug to one of your Pokémon. First time is free, while subsequent times cost ¥200! How about it?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Okay, come back again."))
			release
			end
		}
	}
	if (flag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG) == TRUE) {
		checkmoney(200, 0)
		if (var(VAR_RESULT) == FALSE) {
			msgbox("Hate to break it to you, but you don't have enough money.")
			hidemoneybox
			release
			end
		}
	}
	setvar(VAR_TEMP_0, MOVE_STRUGGLE_BUG)
	setvar(VAR_0x8005, MOVE_STRUGGLE_BUG)
	buffermovename(0, VAR_TEMP_0)
	msgbox("Which Pokémon should learn\n{STR_VAR_1}?", MSGBOX_DEFAULT)
	special(ChooseMonForMoveTutor)
	waitstate
	lock
	faceplayer
	if (var(VAR_RESULT) == 0) {
		msgbox(format("Okay, come back again."))
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("There! It's done. Use that move well!"), MSGBOX_DEFAULT)
	if (flag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG) == FALSE) {
		setflag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG)
	} else {
		removemoney(200)
		updatemoneybox
		msgbox(format("{PLAYER} paid ¥200."), MSGBOX_DEFAULT)
		msgbox(format("Okay, come back again."))
	}
	hidemoneybox
	release
	end
}

script Zone4E_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE4E_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone4E_EventScript_PoisonBarb {
	lock
	if (flag(FLAG_ITEM_ZONE4E_POISON_BARB)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_POISON_BARB)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_POISON_BARB, 1)
	release
	end
}
