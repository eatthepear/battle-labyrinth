mapscripts Zone4E_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE4E_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE4E_POISON_BARB)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone4E_EventScript_KimonoGirl_Exit {
	lockall
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Common_Movement_FaceUp)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	playbgm(MUS_HG_KIMONO_GIRL, 0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	setspeaker("Kimono Girl")
	msgbox(format("Oh, look! The exit!\pThe poor Kimono Girl has been led to the exit by a kind trainer!\pPlease take this as thanks."))
	giveitem(ITEM_SACRED_ASH)
	setspeaker("Kimono Girl")
	msgbox(format("Well, I shall be off.\pIt seems two await you up ahead."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PLAYER, Zone4E_Movement_Player_UnblockKimonoGirl)
	applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, Zone4E_Movement_KimonoGirl_Exit)
	waitmovement(0)
	destroyfollowernpc
	setvar(VAR_ZONE_4_STATE, 2)
	fadedefaultbgm
	releaseall
}

movement Zone4E_Movement_Player_UnblockKimonoGirl {
	walk_down
	walk_right
	face_left
	delay_16 * 4
	face_up
	delay_16 * 6
	face_left
}

movement Zone4E_Movement_KimonoGirl_Exit {
	walk_left
	walk_right
	walk_up * 2
	walk_right * 4
	walk_up * 2
	walk_left * 4
	walk_up
	walk_left * 3
	walk_up
}

script Zone4E_EventScript_LeviathanBattle {
	lockall
	applymovement(LOCALID_BELLA, Common_Movement_WalkDown2)
	applymovement(LOCALID_ELLA, Common_Movement_WalkDown3)
	waitmovement(0)
	applymovement(LOCALID_ELLA, Common_Movement_FaceRight)
	waitmovement(0)
	fadeoutbgm(1)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	delay(16)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(16)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(16)
	setspeaker("Ella")
	msgbox(format("Watch out!"))
	setspeaker("Bella")
	msgbox(format("Don't fear!"))
	setspeaker("Ella")
	msgbox(format("The dynamic duo of Ella & Bella…"))
	setspeaker("Bella")
	msgbox(format("Will always persevere!"))
	setspeaker("Ella & Bella")
	msgbox(format("It's time to battle it out, victory is near!"))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_ELLA_AND_BELLA_LEVIATHAN_4, format("We lost!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setspeaker("Ella")
	msgbox(format("Wargh! Not only are we lost in the wood…"))
	setspeaker("Bella")
	msgbox(format("We could've won that battle, you misunderstood!"))
	setspeaker("Ella")
	msgbox(format("I guess our teamwork could be improved…"))
	setspeaker("Bella")
	msgbox(format("Yeah… Oh right, here's a thing for those approved!"))
	giveitem(ITEM_TM_SNARL)
	setspeaker("Ella")
	msgbox(format("This TM can help in Double Battles!"))
	setspeaker("Bella")
	msgbox(format("Shush, you're annoying them with your prattles!"))
	setspeaker("Ella")
	msgbox(format("That rhyme barely worked!"))
	setspeaker("Bella")
	msgbox(format("Oh, don't be irked!"))
	closemessage
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(LOCALID_ELLA)
	removeobject(LOCALID_BELLA)
	setflag(FLAG_BEAT_LEVIATHAN_4)
	setvar(VAR_ZONE_4_STATE, 3)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	releaseall
	end
}

script Zone4E_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE4E_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone4E_EventScript_PoisonBarb {
	lock
	if (flag(FLAG_ITEM_ZONE4E_POISON_BARB)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_POISON_BARB)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_POISON_BARB, 1)
	release
	end
}
