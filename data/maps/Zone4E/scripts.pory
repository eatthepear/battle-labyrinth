mapscripts Zone4E_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE4E_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE4E_POISON_BARB)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone4E_EventScript_LeviathanBattle {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(3, Common_Movement_WalkRight2)
	applymovement(4, Common_Movement_WalkRight2)
	waitmovement(0)
	fadeoutbgm(1)
	msgbox(Zone4E_Text_Leviathan4Intro)
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if ((flag(FLAG_SETTINGS_BRUTAL) == true) || (flag(FLAG_SETTINGS_CHALLENGE) == true)) {
		trainerbattle_no_intro(TRAINER_PBL_ELLA_AND_BELLA_LEVIATHAN_4_CHALLENGE, Zone4E_Text_Leviathan4Defeat)
	} else {
		trainerbattle_no_intro(TRAINER_PBL_ELLA_AND_BELLA_LEVIATHAN_4, Zone4E_Text_Leviathan4Defeat)
	}
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Ella{COLOR DARK_GRAY}: Wargh! Not only are we lost in the wood…\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: We could've won that battle, you misunderstood!\p{COLOR BLUE}Ella{COLOR DARK_GRAY}: I guess our teamwork could be improved…\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Yeah… Oh right, here's a thing for those approved!"), MSGBOX_DEFAULT)
	giveitem(ITEM_TM_SNARL)
	msgbox(format("{COLOR BLUE}Ella{COLOR DARK_GRAY}: This TM can help in double battles!\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Shush, you're annoying them with your prattles!\p{COLOR BLUE}Ella{COLOR DARK_GRAY}: That rhyme barely worked!\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Oh, don't be irked!"))
	closemessage
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(3)
	removeobject(4)
	setflag(FLAG_BEAT_LEVIATHAN_4)
	setvar(VAR_ZONE_4_STATE, 1)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	releaseall
	end
}

text Zone4E_Text_Leviathan4Intro {
	format("{COLOR BLUE}Ella{COLOR DARK_GRAY}: Watch out!\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Don't fear!\p{COLOR BLUE}Ella{COLOR DARK_GRAY}: The dynamic duo of Ella & Bella…\p{COLOR BLUE}Bella{COLOR DARK_GRAY}: Will always persevere!\p{COLOR BLUE}Ella & Bella{COLOR DARK_GRAY}: It's time to battle it out, victory is near!")
}

text Zone4E_Text_Leviathan4Defeat {
	format("We lost!")
}


script Zone4E_EventScript_StruggleBugTutor {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	if (flag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG) == TRUE) {
		msgbox(format("Hey! You made it to the end. I bet it was a struggle getting here, right?\pYou know what's not a struggle? The move Struggle Bug!\pI can teach Struggle Bug to one of your Pokémon for the low price of ¥500. How about it?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Okay, come back again."))
			release
			end
		}
	} else {
		msgbox(format("Hey! You made it to the end. I bet it was a struggle getting here, right?\pYou know what's not a struggle? The move Struggle Bug!\pI can teach Struggle Bug to one of your Pokémon. First time is free, while subsequent times cost ¥500! How about it?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Okay, come back again."))
			release
			end
		}
	}
	if (flag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG) == TRUE) {
		checkmoney(500, 0)
		if (var(VAR_RESULT) == FALSE) {
			msgbox("Hate to break it to you, but you don't have enough money.")
			hidemoneybox
			release
			end
		}
	}
	setvar(VAR_TEMP_0, MOVE_STRUGGLE_BUG)
	setvar(VAR_0x8005, MOVE_STRUGGLE_BUG)
	buffermovename(0, VAR_TEMP_0)
	msgbox("Which Pokémon should learn\n{STR_VAR_1}?", MSGBOX_DEFAULT)
	special(ChooseMonForMoveTutor)
	waitstate
	lock
	faceplayer
	if (var(VAR_RESULT) == 0) {
		msgbox(format("Okay, come back again."))
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("There! It's done. Use that move well!"), MSGBOX_DEFAULT)
	if (flag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG) == FALSE) {
		setflag(FLAG_ZONE4E_MOVE_TUTOR_TAUGHT_STRUGGLE_BUG)
	} else {
		removemoney(500)
		updatemoneybox
		msgbox(format("{PLAYER} paid ¥500."), MSGBOX_DEFAULT)
		msgbox(format("Okay, come back again."))
	}
	hidemoneybox
	release
	end
}

script Zone4E_EventScript_ItemRandom1 {
	lock
	if (flag(FLAG_ITEM_ZONE4E_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone4E_EventScript_ItemPoisonBarb {
	lock
	if (flag(FLAG_ITEM_ZONE4E_POISON_BARB)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4E_POISON_BARB)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_POISON_BARB, 1)
	release
	end
}
