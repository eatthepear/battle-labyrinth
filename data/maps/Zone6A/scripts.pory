mapscripts Zone6A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE6A_NET_BALL)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE6A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE6A_NEST_BALL)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE6A_TM_BULLDOZE)) {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_TM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_TM_CHEST)
		}
	}
}

script Zone6A_EventScript_LeviathanBattle {
	lockall
	applymovement(LOCALID_MINA, Common_Movement_FaceDown)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	setspeaker("Mina")
	msgbox(format("Woah!\pYou're trying to sneak past me to get to my family's treasures?\pNo can do!\pYou really want to be taught a lesson?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		setspeaker("Mina")
		msgbox(format("That's what I thought!"))
		applymovement(LOCALID_PLAYER, Common_Movement_WalkLeft)
		applymovement(LOCALID_MINA, Common_Movement_FaceLeft)
		waitmovement(0)
		release
		end
	}
	fadeoutbgm(1)
	setspeaker("Mina")
	msgbox(format("Who do you think you are?"))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	if (var(VAR_DIFFICULTY) == DIFFICULTY_BRUTAL) {
		setweather(WEATHER_RAIN)
		doweather
		msgbox("It suddenly began to rain!")
	}
	closemessage
	trainerbattle_no_intro(TRAINER_PBL_MINA, "Rain, rain, go away…")
	setspeaker("Mina")
	msgbox(format("Uh, sorry I was so angry at you.\pI didn't mean to… You're just passing by. Sorry!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_COBY, Common_Movement_ExclamationMark)
	applymovement(LOCALID_ZACK, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	delay(16)
	applymovement(LOCALID_MINA, Zone6A_Movement_Mina_MoveOutOfWay)
	applymovement(LOCALID_COBY, Zone6A_Movement_Coby_WalkUp)
	applymovement(LOCALID_ZACK, Zone6A_Movement_Zack_WalkUp)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	setspeaker("Zack")
	msgbox(format("Hey! No one messes with our family! Take this! Pyukumuku, attack!"))
	setspeaker("Mina")
	msgbox(format("Wait! It isn't their fault!"))
	trainerbattle_no_intro(TRAINER_PBL_ZACK, format("Pyukumuku… Can't you just attack normally?"))
	setspeaker("Coby")
	msgbox(format("Hey! You're messing with my little brother and sister!\pI'll teach you a lesson!"))
	setspeaker("Mina")
	msgbox(format("No! Stop it!"))
	trainerbattle_no_intro(TRAINER_PBL_COBY, format("Our family loses together!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	resetweather
	doweather
	setspeaker("Coby")
	msgbox(format("Whew! You're a tough nut to crack. But we'll crush you for messing with our family!"))
	setspeaker("Mina")
	msgbox(format("Wait! Just listen!"))
	applymovement(LOCALID_COBY, Common_Movement_ExclamationMark)
	applymovement(LOCALID_ZACK, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	delay(16)
	applymovement(LOCALID_COBY, Common_Movement_FaceRight)
	applymovement(LOCALID_ZACK, Common_Movement_FaceRight)
	waitmovement(0)
	setspeaker("Zack")
	msgbox(format("Shouldn't we go after this person, Mina? They're after the treasure!"))
	setspeaker("Mina")
	msgbox(format("I've been trying to tell you, they've just been trying to pass through!"))
	applymovement(LOCALID_COBY, Common_Movement_ExclamationMark)
	applymovement(LOCALID_ZACK, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	delay(16)
	setspeaker("Coby")
	msgbox(format("Oh. Uh…"))
	setspeaker("Zack")
	msgbox(format("This is awkward."))
	applymovement(LOCALID_COBY, Common_Movement_FaceLeft)
	applymovement(LOCALID_ZACK, Common_Movement_FaceLeft)
	waitmovement(0)
	setspeaker("Zack")
	msgbox(format("We're, um…"))
	setspeaker("Coby")
	msgbox(format("We're very sorry for antagonizing you. Really, very sorry.\pHere, take some fresh Berry Juice!"))
	giveitem(ITEM_BERRY_JUICE, 5)
	setspeaker("Mina")
	msgbox(format("C'mon, guys, this isn't enough. We were so mean to them…"))
	setspeaker("Zack")
	msgbox(format("Maybe we can share the treasure?\pWe got most of it out of the sand already anyways."))
	setspeaker("Coby")
	msgbox(format("Good idea.\pYou can have what's left of the treasure. We've already gotten our fair share."))
	setspeaker("Mina")
	msgbox(format("We're really sorry, again. We didn't mean to be so mean."))
	setspeaker("Coby")
	msgbox(format("Well, you live and you learn."))
	setflag(FLAG_BEAT_LEVIATHAN_6)
	setvar(VAR_ZONE_6_STATE, 1)
	applymovement(LOCALID_COBY, Zone6A_Movement_Coby_WalkDown)
	applymovement(LOCALID_ZACK, Zone6A_Movement_Zack_WalkDown)
	waitmovement(0)
	release
	end
}

movement Zone6A_Movement_Mina_MoveOutOfWay {
	walk_right * 2
	face_left
}

movement Zone6A_Movement_Coby_WalkUp {
	walk_up * 3
	face_left
}

movement Zone6A_Movement_Zack_WalkUp {
	walk_left
	walk_up * 2
	face_left
}

movement Zone6A_Movement_Coby_WalkDown {
	walk_down * 3
}

movement Zone6A_Movement_Zack_WalkDown {
	walk_down * 2
	walk_right
	face_down
}

script Zone6A_EventScript_Mina {
	lock
	faceplayer
	setspeaker("Mina")
	msgbox(format("I'm here guarding some of the treasures our family dug up!"))
	release
	end
}

script Zone6A_EventScript_Coby {
	lock
	faceplayer
	setspeaker("Coby")
	msgbox(format("I'm in charge of my younger siblings, though they get on my temper sometimes!"))
	release
	end
}

script Zone6A_EventScript_Zack {
	lock
	faceplayer
	setspeaker("Zack")
	msgbox(format("The tubing life is really sweet!"))
	release
	end
}

script Zone6A_EventScript_TradeDelibird {
	lock
	if (flag(FLAG_ZONE6A_TRADED_DELIBIRD)) {
		faceplayer
		msgbox(format("My Delibird is stronger than other Delibird, but it still stinks!"))
		release
		end
	}
	msgbox(format("Please, oh please, won't anyone trade me a better Pokémon?"))
	applymovement(18, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	faceplayer
	setvar(VAR_0x8008, INGAME_TRADE_DELIBIRD)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Please! You've got to trade me! I'll take anything! Delibird is the worst Pokémon ever!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("Please! I can't win any battles with a Delibird!"))
		release
		end
	}
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		msgbox(format("Please! I can't win any battles with a Delibird!"))
		release
		end
	}
	
	specialvar(VAR_RESULT, IsPokemonFainted)
	if (var(VAR_RESULT) == TRUE) {
		msgbox(format("Hey! Don't trade me a fainted Pokémon!"))
		release
		end
	}
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Thank you! Now I've got a real Pokémon to use!"))
	setflag(FLAG_ZONE6A_TRADED_DELIBIRD)
	release
	end
}

script Zone6A_EventScript_NetBall {
	lock
	if (flag(FLAG_ITEM_ZONE6A_NET_BALL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE6A_NET_BALL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_NET_BALL, 1)
	release
	end
}

script Zone6A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE6A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE6A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone6A_EventScript_NestBall {
	lock
	if (flag(FLAG_ITEM_ZONE6A_NEST_BALL)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE6A_NEST_BALL)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_NEST_BALL, 1)
	release
	end
}

script Zone6A_EventScript_TMBulldoze {
	lock
	if (flag(FLAG_ITEM_ZONE6A_TM_BULLDOZE)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_TM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE6A_TM_BULLDOZE)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM_BULLDOZE)
	release
	end
}

raw `

Zone6A_EventScript_Gilbert::
	trainerbattle_single TRAINER_PBL_GILBERT, Zone6A_Text_GilbertIntro, Zone6A_Text_GilbertDefeat
	msgbox Zone6A_Text_GilbertPostBattle, MSGBOX_AUTOCLOSE
	end

Zone6A_Text_GilbertIntro:
	.string "You made me miss my catch!$"

Zone6A_Text_GilbertDefeat:
	.string "Argh, there was no catch!\n"
	.string "I'm just frustrated!$"

Zone6A_Text_GilbertPostBattle:
	.string "Sorry for taking out my fishing\n"
	.string "frustration out on you.$"

Zone6A_EventScript_Melanie::
	trainerbattle_single TRAINER_PBL_MELANIE, Zone6A_Text_MelanieIntro, Zone6A_Text_MelanieDefeat
	msgbox Zone6A_Text_MelaniePostBattle, MSGBOX_AUTOCLOSE
	end

Zone6A_Text_MelanieIntro:
	.string "Hey, have you ever ridden a horse\n"
	.string "before?$"

Zone6A_Text_MelanieDefeat:
	.string "You should try horseback riding\n"
	.string "sometime!$"

Zone6A_Text_MelaniePostBattle:
	.string "Now that I think about it,\n"
	.string "what even is a horse?\p"
	.string "I mean, we call them horse Pokémon,\n"
	.string "but like, what is a horse?$"

Zone6A_EventScript_Constance::
	trainerbattle_single TRAINER_PBL_CONSTANCE, Zone6A_Text_ConstanceIntro, Zone6A_Text_ConstanceDefeat
	msgbox Zone6A_Text_ConstancePostBattle, MSGBOX_AUTOCLOSE
	end

Zone6A_Text_ConstanceIntro:
	.string "Want to join my picnic?$"

Zone6A_Text_ConstanceDefeat:
	.string "Oops, I forgot to pack food!$"

Zone6A_Text_ConstancePostBattle:
	.string "I can look for some Berries or\n"
	.string "something…$"

Zone6A_EventScript_Frank::
	trainerbattle_single TRAINER_PBL_FRANK, Zone6A_Text_FrankIntro, Zone6A_Text_FrankDefeat
	msgbox Zone6A_Text_FrankPostBattle, MSGBOX_AUTOCLOSE
	end

Zone6A_Text_FrankIntro:
	.string "The beach is full of surprises!$"

Zone6A_Text_FrankDefeat:
	.string "This time, I was surprised by you!$"

Zone6A_Text_FrankPostBattle:
	.string "Your skills are astounding!$"

Zone6A_EventScript_Daisy::
	trainerbattle_single TRAINER_PBL_DAISY, Zone6A_Text_DaisyIntro, Zone6A_Text_DaisyDefeat
	msgbox Zone6A_Text_DaisyPostBattle, MSGBOX_AUTOCLOSE
	end

Zone6A_Text_DaisyIntro:
	.string "These Pokémon are just so cute!$"

Zone6A_Text_DaisyDefeat:
	.string "They're not too great in battle,\n"
	.string "though.$"

Zone6A_Text_DaisyPostBattle:
	.string "I guess these little ones need more\n"
	.string "time to get stronger and evolve!$"

Zone6A_EventScript_VicandDell::
	trainerbattle_double TRAINER_PBL_VIC_AND_DELL, Zone6A_Text_VicandDellIntro, Zone6A_Text_VicandDellDefeat, gText_NotEnoughMons
	msgbox Zone6A_Text_VicandDellPostBattle
	release
	end

Zone6A_Text_VicandDellIntro:
	.string "Vic: We're strong!\p"
	.string "Dell: We work together!$"

Zone6A_Text_VicandDellDefeat:
	.string "Dell: Aww!\p"
	.string "Vic: Aww!$"

Zone6A_Text_VicandDellPostBattle:
	.string "Vic: Losing has its pluses!\p"
	.string "Dell: And its minuses!$"
`
