mapscripts Zone4A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE4A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE4A_SILVER_POWDER)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone4A_EventScript_KimonoGirl_Intro {
	lockall
	playbgm(MUS_HG_KIMONO_GIRL, 0)
	applymovement(LOCALID_KIMONO_GIRL_4A, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(LOCALID_KIMONO_GIRL_4A, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	setspeaker("Kimono Girl")
	msgbox(format("Oh, poor Kimono Girl, all alone in these woods.\pWon't you help show me the way out?"), MSGBOX_AUTOCLOSE)
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_1) == 3) {
		applymovement(LOCALID_KIMONO_GIRL_4A, Common_Movement_WalkUp)
		waitmovement(0)
		applymovement(LOCALID_KIMONO_GIRL_4A, Common_Movement_FaceLeft)
		waitmovement(0)
	}
	applymovement(LOCALID_KIMONO_GIRL_4A, Common_Movement_WalkLeft3)
	waitmovement(0)
	setspeaker("Kimono Girl")
	msgbox(Zone4A_Text_KimonoGirl, MSGBOX_AUTOCLOSE)
	setfollowernpc(LOCALID_KIMONO_GIRL_4A, FOLLOWER_NPC_FLAG_ALL)
	applymovement(LOCALID_KIMONO_GIRL_4A, Common_Movement_FaceLeft)
	waitmovement(0)
	setvar(VAR_ZONE_4_STATE, 1)
	fadedefaultbgm
	releaseall
}

text Zone4A_Text_KimonoGirl {
	format("It seems fate has smiled upon me today, meeting you.\pI do hope we find our way through these woods soon…")
}

script Zone4A_EventScript_KimonoGirl {
	lock
	faceplayer
	setspeaker("Kimono Girl")
	msgbox(Zone4A_Text_KimonoGirl, MSGBOX_AUTOCLOSE)
	release
	end
}

script Zone4A_EventScript_StruggleBugTutor {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	msgbox(format("You found my secret hideout…\pI bet it was a struggle getting here, right?\pI can teach your Pokémon to Struggle Bug, or to Pounce!"))
	message(format("What move should I teach?\nThey will cost you ¥400."))
	waitmessage
	dynmultipush("Struggle Bug", MOVE_STRUGGLE_BUG)
	dynmultipush("Pounce", MOVE_POUNCE)
	dynmultipush("Exit", 1)
	dynmultistack(21, 0, FALSE, 5, 0, 0, DYN_MULTICHOICE_CB_NONE)
	switch (var(VAR_RESULT)) {
		case MOVE_STRUGGLE_BUG:
			setvar(VAR_TEMP_0, MOVE_STRUGGLE_BUG)
			setvar(VAR_0x8005, MOVE_STRUGGLE_BUG)
		case MOVE_POUNCE:
			setvar(VAR_TEMP_0, MOVE_POUNCE)
			setvar(VAR_0x8005, MOVE_POUNCE)
		case 1:
		case MULTI_B_PRESSED:
			hidemoneybox
			msgbox(Zone4A_StruggleBugTutor_ComeBack)
			release
			end
	}
	checkmoney(400, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	buffermovename(0, VAR_TEMP_0)
	msgbox("Which Pokémon should learn\n{STR_VAR_1}?")
	special(ChooseMonForMoveTutor)
	waitstate
	lock
	faceplayer
	if (var(VAR_RESULT) == 0) {
		msgbox(Zone4A_StruggleBugTutor_ComeBack)
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("There! It's done. Use that move well!"))
	removemoney(400)
	updatemoneybox
	msgbox(format("{PLAYER} paid ¥400."))
	hidemoneybox
	msgbox(Zone4A_StruggleBugTutor_ComeBack)
	release
	end
}

text Zone4A_StruggleBugTutor_ComeBack {
	format("Hope you don't struggle to get through the woods!")
}

script Zone4A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE4A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone4A_EventScript_SilverPowder {
	lock
	if (flag(FLAG_ITEM_ZONE4A_SILVER_POWDER)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE4A_SILVER_POWDER)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SILVER_POWDER, 1)
	release
	end
}

raw `
Zone4A_EventScript_Herman::
	trainerbattle_single TRAINER_PBL_HERMAN, Zone4A_Text_HermanIntro, Zone4A_Text_HermanDefeat
	msgbox Zone4A_Text_HermanPostBattle, MSGBOX_AUTOCLOSE
	end

Zone4A_Text_HermanIntro:
	.string "I keep going through this exit,\n"
	.string "'cause it might lead somewhere!$"

Zone4A_Text_HermanDefeat:
	.string "But I keep getting back here!$"

Zone4A_Text_HermanPostBattle:
	.string "Try something else?\n"
	.string "No, I'm sure this is the right way!$"

`
