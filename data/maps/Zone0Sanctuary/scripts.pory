mapscripts Zone0Sanctuary_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ZONE, 0: Zone0Sanctuary_EventScript_Intro
	]
}

script Zone0Sanctuary_EventScript_Intro {
    lockall
	clearflag(FLAG_SETTINGS_NUZLOCKE)
	setflag(FLAG_SYS_B_DASH)
	setflag(FLAG_SYS_POKEMON_GET)
	setflag(FLAG_SYS_PC_LANETTE)
	setflag(FLAG_AUTORUN_TOGGLE)
	call(EventScript_MoveCameraUp)
	applymovement(1, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: Ah. You've arrived."), MSGBOX_AUTOCLOSE)
	applymovement(1, Common_Movement_WalkDown3)
	call(EventScript_MoveCameraDown)
	waitmovement(0)
	msgbox("Welcome to the\n{COLOR RED}Pokémon Battle Labyrinth{COLOR DARK_GRAY}.\pI am your guide for braving the\nchallenges ahead.\pWould you like to listen to the tutorial?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		setvar(VAR_TEMP_5, 1) // VAR_TEMP_5: 1 means yes tutorial
		msgbox(format("The Pokémon Battle Labyrinth is comprised of many {COLOR RED}Zones{COLOR DARK_GRAY} you must traverse.\p"
		"In these Zones, you might encounter wild Pokémon, great treasures, tough trainers, and more.\p"
		"Each Zone typically contains a {COLOR RED}Leviathan{COLOR DARK_GRAY}, a boss that yields great rewards if defeated.\p"
		"At the end of each Zone and before the next Zone begins, you will find a {COLOR RED}Sanctuary{COLOR DARK_GRAY}, like this one, with shops to purchase supplies, heal your Pokémon, and other benefits.\p"
		"Once you enter a new Zone, you will not be able to access a Sanctuary until you exit that Zone.\p"
		"In order to win, you must progress through all Zones without losing a battle."), MSGBOX_AUTOCLOSE)
		delay(16)
	}
	questmenu(QUEST_MENU_SET_ACTIVE, QUEST_PBL)
	setflag(FLAG_SETTINGS_EXP_ALL)
	msgbox(format("Pokémon Battle Labyrinth has many settings that are customizable, including {COLOR RED}Nuzlocke{COLOR DARK_GRAY} mode,\l{COLOR RED}Randomizer{COLOR DARK_GRAY} mode, and difficulty options.\pWould you like to modify these settings now from the default?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		setvar(VAR_TEMP_1, 1) // VAR_TEMP_1: 1 means dealing w/ settings menu, 2 means done w/ it
		msgbox(format("Note that some settings cannot be adjusted once the challenge has started.\pIf you're looking to play the hardest game mode, {COLOR RED}Brutal{COLOR DARK_GRAY} automatically enables most of the difficulty settings.\pOtherwise, information about each individual setting can be found by selecting it."), MSGBOX_DEFAULT)
		while (var(VAR_TEMP_1) == 1) {
			call(EventScript_Settings)
			msgbox(format("Are you sure you are done selecting your settings?"), MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES) {
				setvar(VAR_TEMP_1, 0)
			}
		}
		msgbox(format("Settings saved!"), MSGBOX_DEFAULT)
	}
	delay(16)
	if (var(VAR_TEMP_5) == 1) {
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: Allow me to show you each of the shops."), MSGBOX_AUTOCLOSE)
		applymovement(1, Zone0Sanctuary_Movement_Colress_FirstShop)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_FirstShop)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This shop is {COLOR RED}Portentia's Potions{COLOR DARK_GRAY}.\p"
		"Healing supplies, such as Potions or other curative measures, can be found here."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_SecondShop)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_SecondShop)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This shop is {COLOR RED}Tim's Pokéball Shop{COLOR DARK_GRAY}.\p"
		"They have a variety of Pokéballs to buy."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_ThirdShop)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_ThirdShop)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This shop is {COLOR RED}Julia's Berries{COLOR DARK_GRAY}.\p"
		"Julia is a legendary chef who curates a selection of Berries for Pokémon."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_FourthShop)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_FourthShop)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This shop is {COLOR RED}Marie's Egg Delivery Service{COLOR DARK_GRAY}.\p"
		"Marie helps match Pokémon eggs with new owners."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_FifthShop)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_FifthShop)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This shop is {COLOR RED}Varrick's Extravagant Emporium{COLOR DARK_GRAY}.\p"
		"A huge variety of items are sold here."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_SixthShop)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_SixthShop)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This shop is {COLOR RED}Lelouch's Services Shop{COLOR DARK_GRAY}.\p"
		"He only sees customers who can pay his price."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_ToPokemonCenter)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_ToPokemonCenter)
		waitmovement(0)
		special(SpawnCameraObject)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: This is the {COLOR RED}Pokémon Center{COLOR DARK_GRAY}, though don't let Joy catch you saying that.\pYour Pokémon can be healed at a flat fee.\pIn addition, inside you might find people with advice or aid for you.\pNote the building has a back entrance for ease of access.\pYou can also access {COLOR RED}Zone 0{COLOR DARK_GRAY} on the right inside."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown2)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkLeft)
		waitmovement(0)
		special(RemoveCameraObject)
		applymovement(1, Zone0Sanctuary_Movement_Colress_BackMid)
		applymovement(OBJ_EVENT_ID_PLAYER, Zone0Sanctuary_Movement_Player_BackMid)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: Our tour has finished.\p"
		"These shops will be available at every Sanctuary.\p"
		"As you progress through more Zones, more items and services will unlock.\p"
		"Also, it is highly recommended you stock up on any supplies before entering the next Zone."), MSGBOX_AUTOCLOSE)
		delay(16)
	}
	if (flag(FLAG_SETTINGS_RANDOMIZER) == true) {
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: Because the {COLOR RED}Randomizer{COLOR DARK_GRAY} is on, you get a random starter."))
		callnative(GiveRandomStarter)
		playfanfare(MUS_OBTAIN_ITEM)
		bufferleadmonspeciesname(0)
		message(format("{PLAYER} received the {STR_VAR_1}!"))
		waitmessage
		waitfanfare
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: How about a nickname for your {STR_VAR_1}?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			setvar(VAR_0x8004, 0)
			call(Common_EventScript_NameReceivedPartyMon)
		}
		goto(Zone0Sanctuary_EventScript_EndIntro)
	}
	if (flag(FLAG_SETTINGS_BRUTAL) == true) {
		msgbox(format("As a new challenger, you will receive a starter Pokémon.\p"
		"On Brutal difficulty, your choice is limited from the pool of possible starters to only three options.\p"
		"Choose carefully as these Pokémon are very rare."))
		special(ChooseStarter)
		waitstate
		bufferleadmonspeciesname(0)
		msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: How about a nickname for your {STR_VAR_1}?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			setvar(VAR_0x8004, 0)
			call(Common_EventScript_NameReceivedPartyMon)
		}
		goto(Zone0Sanctuary_EventScript_EndIntro)
	} else {
		msgbox(format("As a new challenger, you will receive a starter Pokémon.\p"
		"You may have your choice of any of the generation 1-9 starter Pokémon.\p"
		"Choose carefully as these Pokémon are very rare."))
		goto(Zone0Sanctuary_EventScript_HandleStarterSelectionDefault)
	}
}

script Zone0Sanctuary_EventScript_HandleStarterSelectionDefault {
	hidemonpic
	message(format("Choose a starter Pokémon."))
	dynmultichoice(0, 0, FALSE, 5, 0, DYN_MULTICHOICE_CB_NONE, "Bulbasaur", "Charmander", "Squirtle", "Chikorita", "Cyndaquil", "Totodile", "Treecko", "Torchic", "Mudkip", "Turtwig", "Chimchar", "Piplup", "Snivy", "Tepig", "Oshawott", "Chespin", "Fennekin", "Froakie", "Rowlet", "Litten", "Popplio", "Grookey", "Scorbunny", "Sobble", "Sprigatito", "Fuecoco", "Quaxly", "Random")
	switch (var(VAR_RESULT)) {
		case 0:
			setvar(VAR_TEMP_3, SPECIES_BULBASAUR)
		case 1:
			setvar(VAR_TEMP_3, SPECIES_CHARMANDER)
		case 2:
			setvar(VAR_TEMP_3, SPECIES_SQUIRTLE)
		case 3:
			setvar(VAR_TEMP_3, SPECIES_CHIKORITA)
		case 4:
			setvar(VAR_TEMP_3, SPECIES_CYNDAQUIL)
		case 5:
			setvar(VAR_TEMP_3, SPECIES_TOTODILE)
		case 6:
			setvar(VAR_TEMP_3, SPECIES_TREECKO)
		case 7:
			setvar(VAR_TEMP_3, SPECIES_TORCHIC)
		case 8:
			setvar(VAR_TEMP_3, SPECIES_MUDKIP)
		case 9:
			setvar(VAR_TEMP_3, SPECIES_TURTWIG)
		case 10:
			setvar(VAR_TEMP_3, SPECIES_CHIMCHAR)
		case 11:
			setvar(VAR_TEMP_3, SPECIES_PIPLUP)
		case 12:
			setvar(VAR_TEMP_3, SPECIES_SNIVY)
		case 13:
			setvar(VAR_TEMP_3, SPECIES_TEPIG)
		case 14:
			setvar(VAR_TEMP_3, SPECIES_OSHAWOTT)
		case 15:
			setvar(VAR_TEMP_3, SPECIES_CHESPIN)
		case 16:
			setvar(VAR_TEMP_3, SPECIES_FENNEKIN)
		case 17:
			setvar(VAR_TEMP_3, SPECIES_FROAKIE)
		case 18:
			setvar(VAR_TEMP_3, SPECIES_ROWLET)
		case 19:
			setvar(VAR_TEMP_3, SPECIES_LITTEN)
		case 20:
			setvar(VAR_TEMP_3, SPECIES_POPPLIO)
		case 21:
			setvar(VAR_TEMP_3, SPECIES_GROOKEY)
		case 22:
			setvar(VAR_TEMP_3, SPECIES_SCORBUNNY)
		case 23:
			setvar(VAR_TEMP_3, SPECIES_SOBBLE)
		case 24:
			setvar(VAR_TEMP_3, SPECIES_SPRIGATITO)
		case 25:
			setvar(VAR_TEMP_3, SPECIES_FUECOCO)
		case 26:
			setvar(VAR_TEMP_3, SPECIES_QUAXLY)
		case 27:
			setvar(VAR_TEMP_3, 0)
	}
	if (var(VAR_TEMP_3) == 0) {
		playse(SE_CONTEST_PLACE)
		message(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: Randomizing…"))
		delay(96)
		waitse
		waitmessage
		random(24)
		switch (var(VAR_RESULT)) {
			case 0:
				setvar(VAR_TEMP_3, SPECIES_BULBASAUR)
			case 1:
				setvar(VAR_TEMP_3, SPECIES_CHARMANDER)
			case 2:
				setvar(VAR_TEMP_3, SPECIES_SQUIRTLE)
			case 3:
				setvar(VAR_TEMP_3, SPECIES_CHIKORITA)
			case 4:
				setvar(VAR_TEMP_3, SPECIES_CYNDAQUIL)
			case 5:
				setvar(VAR_TEMP_3, SPECIES_TOTODILE)
			case 6:
				setvar(VAR_TEMP_3, SPECIES_TREECKO)
			case 7:
				setvar(VAR_TEMP_3, SPECIES_TORCHIC)
			case 8:
				setvar(VAR_TEMP_3, SPECIES_MUDKIP)
			case 9:
				setvar(VAR_TEMP_3, SPECIES_TURTWIG)
			case 10:
				setvar(VAR_TEMP_3, SPECIES_CHIMCHAR)
			case 11:
				setvar(VAR_TEMP_3, SPECIES_PIPLUP)
			case 12:
				setvar(VAR_TEMP_3, SPECIES_SNIVY)
			case 13:
				setvar(VAR_TEMP_3, SPECIES_TEPIG)
			case 14:
				setvar(VAR_TEMP_3, SPECIES_OSHAWOTT)
			case 15:
				setvar(VAR_TEMP_3, SPECIES_CHESPIN)
			case 16:
				setvar(VAR_TEMP_3, SPECIES_FENNEKIN)
			case 17:
				setvar(VAR_TEMP_3, SPECIES_FROAKIE)
			case 18:
				setvar(VAR_TEMP_3, SPECIES_ROWLET)
			case 19:
				setvar(VAR_TEMP_3, SPECIES_LITTEN)
			case 20:
				setvar(VAR_TEMP_3, SPECIES_POPPLIO)
			case 21:
				setvar(VAR_TEMP_3, SPECIES_GROOKEY)
			case 22:
				setvar(VAR_TEMP_3, SPECIES_SCORBUNNY)
			case 23:
				setvar(VAR_TEMP_3, SPECIES_SOBBLE)
		}
	}
	bufferspeciesname(0, VAR_TEMP_3)
	showmonpic(VAR_TEMP_3, 10, 3)
	playmoncry(VAR_TEMP_3, 0)
	msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: You want {STR_VAR_1}?"), MSGBOX_YESNO)
	waitmoncry
	if (var(VAR_RESULT) == NO) {
		setvar(VAR_TEMP_3, 0)
		goto(Zone0Sanctuary_EventScript_HandleStarterSelectionDefault)
	}
	switch(var(VAR_TEMP_3)) {
		case SPECIES_BULBASAUR:
		case SPECIES_CHIKORITA:
		case SPECIES_TREECKO:
		case SPECIES_TURTWIG:
		case SPECIES_SNIVY:
		case SPECIES_CHESPIN:
		case SPECIES_ROWLET:
		case SPECIES_GROOKEY:
		case SPECIES_SPRIGATITO:
			setvar(VAR_STARTER_MON, 0)
		case SPECIES_CHARMANDER:
		case SPECIES_CYNDAQUIL:
		case SPECIES_TORCHIC:
		case SPECIES_CHIMCHAR:
		case SPECIES_TEPIG:
		case SPECIES_FENNEKIN:
		case SPECIES_LITTEN:
		case SPECIES_SCORBUNNY:
		case SPECIES_FUECOCO:
			setvar(VAR_STARTER_MON, 1)
		case SPECIES_SQUIRTLE:
		case SPECIES_TOTODILE:
		case SPECIES_MUDKIP:
		case SPECIES_PIPLUP:
		case SPECIES_OSHAWOTT:
		case SPECIES_FROAKIE:
		case SPECIES_POPPLIO:
		case SPECIES_SOBBLE:
		case SPECIES_QUAXLY:
			setvar(VAR_STARTER_MON, 2)
	}
	givemon(VAR_TEMP_3, 5, ITEM_NONE)
	hidemonpic
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	bufferleadmonspeciesname(0)
	msgbox(format("{COLOR BLUE}Colress{COLOR DARK_GRAY}: How about a nickname for your {STR_VAR_1}?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		setvar(VAR_0x8004, 0)
		call(Common_EventScript_NameReceivedPartyMon)
	}
	goto(Zone0Sanctuary_EventScript_EndIntro)
}

script Zone0Sanctuary_EventScript_EndIntro {
	if (var(VAR_TEMP_5) == 1) {
		msgbox(format("Excellent choice.\p""You can also re-nickname your Pokémon from the party menu."))
		delay(16)
		if (flag(FLAG_SETTINGS_INFINITE)) {
			msgbox(format("Because you are playing on {COLOR RED}Infinite{COLOR DARK_GRAY} mode, here are the items you'd need.\pYou can get more by asking me about {COLOR RED}Infinite{COLOR DARK_GRAY} mode again."), MSGBOX_DEFAULT)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox(format("Obtained Full Restores, Max Elixirs, and Max Repels!"), MSGBOX_DEFAULT)
			waitfanfare
		} else {
			msgbox(format("I also have a beginner's kit with items and cash to get you started."), MSGBOX_DEFAULT)
			giveitem(ITEM_POKE_BALL, 15)
			giveitem(ITEM_POTION, 10)
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox(format("Received ¥5000!"), MSGBOX_DEFAULT)
			waitfanfare
		}
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox(format("Received the {COLOR RED}Pokédex{COLOR DARK_GRAY}!"), MSGBOX_DEFAULT)
		waitfanfare
		closemessage
		delay(16)
	} else {
		if (flag(FLAG_SETTINGS_INFINITE)) {
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_FULL_RESTORE, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_ELIXIR, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
			additem(ITEM_MAX_REPEL, 100)
		} else {
			additem(ITEM_POTION, 10)
		}
		additem(ITEM_POKE_BALL, 15)
	}
	setflag(FLAG_SYS_POKEDEX_GET)
	special(SetUnlockedPokedexFlags)
	setmonmetlocation(0, 255)
	setflag(FLAG_SYS_NATIONAL_DEX)
	special(EnableNationalPokedex)
	initclock(1, 1)
	callnative(InitTimeBasedEvents)
	setvar(VAR_ZONE, 1)
	buffernumberstring(0, VAR_ZONE)
	msgbox(gText_Colress_WhenUReady, MSGBOX_AUTOCLOSE)
	applymovement(1, Common_Movement_Colress_WalkUp)
	waitmovement(1)
	setflag(FLAG_IS_DEBUGGING_SAVEFILE) // to remove for release
	releaseall
	end
}

script Zone0Sanctuary_EventScript_NextZone {
	lock
	faceplayer
	message(gText_Colress_Greetings)
	waitmessage
	dynmultichoice(0, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Next Zone", "Settings", "Documentation", "Exit")
	switch (var(VAR_RESULT)) {
		case 0:
			buffernumberstring(0, VAR_ZONE)
			msgbox(gText_Colress_SureReadyToLeave, MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES) {
				call(EventScript_LeavingSetup)
				warp(MAP_ZONE1A, 255, 16, 28)
				waitstate
			}
		case 1:
			goto(EventScript_Settings)
		case 2:
			goto(EventScript_Documentation)
	}
	goto(EventScript_WhenUReady)
}

movement Zone0Sanctuary_Movement_Colress_FirstShop {
	walk_left
	walk_down * 6
	walk_left * 15
	face_up
}

movement Zone0Sanctuary_Movement_Player_FirstShop {
	delay_16 * 2
	walk_left
	walk_down * 5
	walk_left * 14
	face_up
}

movement Zone0Sanctuary_Movement_Colress_SecondShop {
	walk_down
	walk_right * 16
	walk_up
	walk_right * 16
	face_up
}

movement Zone0Sanctuary_Movement_Player_SecondShop {
	delay_16 * 2
	walk_down
	walk_right * 15
	walk_up
	walk_right * 15
	face_up
}

movement Zone0Sanctuary_Movement_Colress_ThirdShop {
	walk_right * 3
	walk_down * 15
	walk_left * 8
	face_up
}

movement Zone0Sanctuary_Movement_Player_ThirdShop {
	walk_right * 4
	walk_down * 15
	walk_left * 7
	face_up
}

movement Zone0Sanctuary_Movement_Colress_FourthShop {
	walk_left * 2
	walk_down * 10
	walk_right * 13
	face_up
}

movement Zone0Sanctuary_Movement_Player_FourthShop {
	walk_left * 3
	walk_down * 10
	walk_right * 12
	face_up
}

movement Zone0Sanctuary_Movement_Colress_FifthShop {
	walk_down
	walk_left * 13
	walk_up * 11
	walk_left * 21
	face_up
}

movement Zone0Sanctuary_Movement_Player_FifthShop {
	delay_16 * 2
	walk_down
	walk_left * 12
	walk_up * 11
	walk_left * 20
	face_up
}

movement Zone0Sanctuary_Movement_Colress_SixthShop {
	walk_down
	walk_right * 3
	walk_down * 9
	walk_left * 13
	face_up
}

movement Zone0Sanctuary_Movement_Player_SixthShop {
	delay_16 * 2
	walk_down
	walk_right * 2
	walk_down * 9
	walk_left * 12
	face_up
}

movement Zone0Sanctuary_Movement_Colress_ToPokemonCenter {
	walk_down
	walk_right * 13
	walk_up * 3
	walk_right * 9
	face_up
}

movement Zone0Sanctuary_Movement_Player_ToPokemonCenter {
	delay_16 * 2
	walk_down
	walk_right * 12
	walk_up * 3
	walk_right * 8
	face_up
}

movement Zone0Sanctuary_Movement_Colress_BackMid {
	walk_down
	walk_left * 8
	walk_up * 8
	walk_right * 8
	walk_up * 22
	face_down
}

movement Zone0Sanctuary_Movement_Player_BackMid {
	delay_16 * 2
	walk_down
	walk_left * 7
	walk_up * 8
	walk_right * 8
	walk_up * 21
}
