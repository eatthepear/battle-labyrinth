mapscripts Zone2A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE2A_ANTIDOTE)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone2A_EventScript_ItemAntidote {
	lock
	if (flag(FLAG_ITEM_ZONE2A_ANTIDOTE)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE2A_ANTIDOTE)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_ANTIDOTE, 1)
	release
	end
}

script Zone2A_EventScript_CautionSign {
	msgbox(format("Caution!\pDiglett have been found living inside the tunnel.\pRepels recommended!"), MSGBOX_SIGN)
	end
}

script Zone2A_EventScript_ExpShareSign {
	msgbox(format("Trainer Tips\pThe Exp. All only gives Pokémon who didn't participate in battle 25% experience.\pIf you want to train up a particular Pokémon, you can give them the held item Exp. Share so they get full experience!"), MSGBOX_SIGN)
	end
}

script Zone2A_EventScript_Polly {
	trainerbattle_single(TRAINER_PBL_POLLY, format("I've heard there's a tunnel nearby with some cool Pokémon living in it!"), format("At this rate, I'll never find it!"))
	msgbox(format("I'm sure it's here somewhere…"), MSGBOX_AUTOCLOSE)
	end
}

raw `
Zone2A_EventScript_Jeremiah::
	trainerbattle_single TRAINER_PBL_JEREMIAH, Zone2A_Text_JeremiahIntro, Zone2A_Text_JeremiahDefeat
	msgbox Zone2A_Text_JeremiahPostBattle, MSGBOX_AUTOCLOSE
	end

Zone2A_Text_JeremiahIntro:
	.string "The more legs, the better!$"

Zone2A_Text_JeremiahDefeat:
	.string "Oh… Come on!$"

Zone2A_Text_JeremiahPostBattle:
	.string "Just wait till my Bugs are all evolved!$"

Zone2A_EventScript_Sign::
	msgbox Zone2A_Text_Sign, MSGBOX_SIGN
	end

Zone2A_Text_Sign:
	.string "Trainer Tips\p"
	.string "You may find hidden items by\n"
	.string "checking certain tiles with {A_BUTTON}.\p"
	.string "However, you won't find any hidden\n"
	.string "items on Brutal difficulty.$"
`
