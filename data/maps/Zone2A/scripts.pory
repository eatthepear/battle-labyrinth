mapscripts Zone2A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE2A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE2A_RANDOM_2)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE2A_SILK_SCARF)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE2A_TM_BUG_BITE)) {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_TM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_TM_CHEST)
		}
	}
}

script Zone2A_EventScript_Max {
	lockall
	playbgm(MUS_DP_POKE_MART, FALSE)
	applymovement(LOCALID_MAX, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	switch (var(VAR_TEMP_0)) {
		case 7:
			applymovement(LOCALID_MAX, Common_Movement_WalkLeft2)
			waitmovement(0)
		case 8:
			applymovement(LOCALID_MAX, Common_Movement_WalkLeft)
			waitmovement(0)
		case 10:
			applymovement(LOCALID_MAX, Common_Movement_WalkRight)
			waitmovement(0)
		case 11:
			applymovement(LOCALID_MAX, Common_Movement_WalkRight2)
			waitmovement(0)
	}
	applymovement(LOCALID_MAX, Common_Movement_WalkUp2)
	waitmovement(0)
	setspeaker("???")
	msgbox(format("By my calculations, you should be {PLAYER}!\pHi, {PLAYER}! My name is {COLOR BLUE}Max{COLOR DARK_GRAY}."))
	setspeaker("Max")
	msgbox(format("I am a traveling merchant, selling Technical Machines for your Pokémon!\pI travel a lot, never going to the same place again.\pI'll have different wares every time you see me, so be sure to buy whatever TMs you deem important!"))
	if (var(VAR_DIFFICULTY) < DIFFICULTY_BRUTAL) {
		setspeaker("Max")
		msgbox(format("Tell you what, for a first-time customer like you, I'll give you a TM for free!"))
		giveitem(ITEM_TM_PROTECT)
	}
	call(EventScript_Shop_Max)
	setvar(VAR_ZONE_2_STATE, 1)
	fadedefaultbgm
	releaseall
}

script Zone2A_EventScript_LeviathanBattle {
	lock
	faceplayer
	if (flag(FLAG_BEAT_LEVIATHAN_2) == true) {
		setspeaker("Shaun")
		msgbox(Zone2A_Text_Shaun_Post)
		release
		end
	}
	setspeaker("Shaun")
	msgbox(format("My Pokémon love to eat, just like me!"))
	if (!defeated(TRAINER_PBL_POLLY) || !defeated(TRAINER_PBL_JEREMIAH) || !defeated(TRAINER_PBL_JOSH) || !defeated(TRAINER_PBL_HARPER)) {
		setspeaker("Shaun")
		msgbox(format("Tell you what, if you go and defeat all four of my friends around the farm, I'll show you just how much my Pokémon can eat!"))
		release
		end
	}
	setspeaker("Shaun")
	msgbox(format("My friends tell me you've defeated every last one of them!\pAre you interested in a battle? I'll show you just how much my Pokémon can eat!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		setspeaker("Shaun")
		msgbox(format("Oh, all right…"))
		release
		end
	}
	closemessage
	fadeoutbgm(1)
	setspeaker("Shaun")
	msgbox(format("All right then, let's battle!"))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_SHAUN_LEVIATHAN_2, format("Ack! I lost again!\pDoggone it!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setspeaker("Shaun")
	msgbox(format("Even after all those Berries, it wasn't enough to get across the finish line.\pThat's all right though;\pI enjoyed our battle so much, I want to give you these!"), MSGBOX_DEFAULT)
	giveitem(ITEM_ORAN_BERRY, 5)
	setflag(FLAG_BEAT_LEVIATHAN_2)
	setspeaker("Shaun")
	msgbox(Zone2A_Text_Shaun_Post)
	release
	end
}

text Zone2A_Text_Shaun_Post {
	format("Sometimes Skwovet come looking for Berries and block the road.\pJust wait a little bit, and they should move out of the way shortly!")
}

script Zone2A_EventScript_BuyBug {
	lock
	faceplayer
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true && flag(FLAG_ZONE2A_NUZLOCKE_BOUGHT_BUG) == true) {
		msgbox(format("Scram! I'm out of bugs for you to buy right now.\pCome back later."))
		release
		end
	}
	showmoneybox(18, 0, 0)
	msgbox(format("Would you care to buy a Bug-type Pokémon?\pI've got quite the great selection for only ¥200!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		hidemoneybox
		msgbox(format("You don't want any? Fine. More for me."))
		release
		end
	}
	checkmoney(200, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	setvar(VAR_TEMP_1, SPECIES_WURMPLE)
	setvar(VAR_TEMP_2, SPECIES_SEWADDLE)
	setvar(VAR_TEMP_3, SPECIES_SPINARAK)
	setvar(VAR_TEMP_4, SPECIES_LEDYBA)
	setvar(VAR_TEMP_5, SPECIES_KRICKETOT)
	setvar(VAR_TEMP_6, SPECIES_TAROUNTULA)
	setvar(VAR_TEMP_7, SPECIES_BURMY)
	setvar(VAR_TEMP_8, SPECIES_BLIPBUG)
	setvar(VAR_TEMP_9, SPECIES_COMBEE)
	setvar(VAR_TEMP_A, SPECIES_PARAS)
	setvar(VAR_TEMP_B, SPECIES_KARRABLAST)
	setvar(VAR_TEMP_C, SPECIES_SHELMET)
	setvar(VAR_TEMP_D, SPECIES_SURSKIT)
	setvar(VAR_TEMP_E, SPECIES_RELLOR)
	setvar(VAR_TEMP_F, SPECIES_NINCADA)
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		// Not going to bother checking for species clause, as there's like 15 Pokemon and there's no way you have all 15
		msgbox(gText_Nuzlocke_GiftPossible, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			msgbox(format("Wurmple, Sewaddle, Spinarak, Ledyba, Kricketot, Tarountula, Burmy, Blipbug, Combee, Paras, Karrablast, Shelmet, Surskit, Rellor, or Nincada."))
		}
		msgbox(gText_Nuzlocke_ReadyReceive, MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Alrighty, well, you just come on back if you change your mind."))
			release
			end
		}
		setvar(VAR_TEMP_0, 0)
		while (var(VAR_TEMP_0) == 0) {
			random(15)
			switch (var(VAR_RESULT)) {
				case 0:
					if (checkspeciesclause(VAR_TEMP_1) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_1)
				case 1:
					if (checkspeciesclause(VAR_TEMP_2) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_2)
				case 2:
					if (checkspeciesclause(VAR_TEMP_3) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_3)
				case 3:
					if (checkspeciesclause(VAR_TEMP_4) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_4)
				case 4:
					if (checkspeciesclause(VAR_TEMP_5) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_5)
				case 5:
					if (checkspeciesclause(VAR_TEMP_6) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_6)
				case 6:
					if (checkspeciesclause(VAR_TEMP_7) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_7)
				case 7:
					if (checkspeciesclause(VAR_TEMP_8) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_8)
				case 8:
					if (checkspeciesclause(VAR_TEMP_9) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_9)
				case 9:
					if (checkspeciesclause(VAR_TEMP_A) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_A)
				case 10:
					if (checkspeciesclause(VAR_TEMP_B) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_B)
				case 11:
					if (checkspeciesclause(VAR_TEMP_C) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_C)
				case 12:
					if (checkspeciesclause(VAR_TEMP_D) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_D)
				case 13:
					if (checkspeciesclause(VAR_TEMP_E) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_E)
				case 14:
					if (checkspeciesclause(VAR_TEMP_F) == TRUE) {
						continue
					}
					copyvar(VAR_TEMP_0, VAR_TEMP_F)
			}
		}
	} else {
		message(format("Choose which Bug you would like."))
		bufferspeciesname(0, VAR_TEMP_1)
		dynmultipush("{STR_VAR_1}", 0)
		bufferspeciesname(0, VAR_TEMP_2)
		dynmultipush("{STR_VAR_1}", 1)
		bufferspeciesname(0, VAR_TEMP_3)
		dynmultipush("{STR_VAR_1}", 2)
		bufferspeciesname(0, VAR_TEMP_4)
		dynmultipush("{STR_VAR_1}", 3)
		bufferspeciesname(0, VAR_TEMP_5)
		dynmultipush("{STR_VAR_1}", 4)
		bufferspeciesname(0, VAR_TEMP_6)
		dynmultipush("{STR_VAR_1}", 5)
		bufferspeciesname(0, VAR_TEMP_7)
		dynmultipush("{STR_VAR_1}", 6)
		bufferspeciesname(0, VAR_TEMP_8)
		dynmultipush("{STR_VAR_1}", 7)
		bufferspeciesname(0, VAR_TEMP_9)
		dynmultipush("{STR_VAR_1}", 8)
		bufferspeciesname(0, VAR_TEMP_A)
		dynmultipush("{STR_VAR_1}", 9)
		bufferspeciesname(0, VAR_TEMP_B)
		dynmultipush("{STR_VAR_1}", 10)
		bufferspeciesname(0, VAR_TEMP_C)
		dynmultipush("{STR_VAR_1}", 11)
		bufferspeciesname(0, VAR_TEMP_D)
		dynmultipush("{STR_VAR_1}", 12)
		bufferspeciesname(0, VAR_TEMP_E)
		dynmultipush("{STR_VAR_1}", 13)
		bufferspeciesname(0, VAR_TEMP_F)
		dynmultipush("{STR_VAR_1}", 14)
		dynmultipush("Exit", 15)
		dynmultistack(0, 0, FALSE, 6, 0, 0, DYN_MULTICHOICE_CB_NONE)
		switch (var(VAR_RESULT)) {
			case 0:
				copyvar(VAR_TEMP_0, VAR_TEMP_1)
			case 1:
				copyvar(VAR_TEMP_0, VAR_TEMP_2)
			case 2:
				copyvar(VAR_TEMP_0, VAR_TEMP_3)
			case 3:
				copyvar(VAR_TEMP_0, VAR_TEMP_4)
			case 4:
				copyvar(VAR_TEMP_0, VAR_TEMP_5)
			case 5:
				copyvar(VAR_TEMP_0, VAR_TEMP_6)
			case 6:
				copyvar(VAR_TEMP_0, VAR_TEMP_7)
			case 7:
				copyvar(VAR_TEMP_0, VAR_TEMP_8)
			case 8:
				copyvar(VAR_TEMP_0, VAR_TEMP_9)
			case 9:
				copyvar(VAR_TEMP_0, VAR_TEMP_A)
			case 10:
				copyvar(VAR_TEMP_0, VAR_TEMP_B)
			case 11:
				copyvar(VAR_TEMP_0, VAR_TEMP_C)
			case 12:
				copyvar(VAR_TEMP_0, VAR_TEMP_D)
			case 13:
				copyvar(VAR_TEMP_0, VAR_TEMP_E)
			case 14:
				copyvar(VAR_TEMP_0, VAR_TEMP_F)
			case 15:
			case MULTI_B_PRESSED:
				closemessage
				hidemoneybox
				msgbox(format("You don't want any? Fine. More for me."))
				release
				end
		}
	}
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		msgbox(format("Just to confirm…\pYou're sure you want to buy a Bug for ¥200?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("You don't want any? Fine. More for me."))
			release
			end
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(format("Just to confirm…\pYou're sure you want to buy {STR_VAR_1} for ¥200?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("You don't want any? Fine. More for me."))
			release
			end
		}
	}
	removemoney(200)
	updatemoneybox
	msgbox(format("Ehehehe, thank you…"))
	hidemoneybox
	bufferspeciesname(0, VAR_TEMP_0)
	msgbox("Here's your {STR_VAR_1}.")
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		msgbox(format("Your party is full. Would you like to send a party Pokémon to the PC and add {STR_VAR_1} to your party?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(EventScript_ChoosePartyMonToSend)
		}
	}
	givemon(VAR_TEMP_0, 6, metLocation=METLOC_BOUGHT_BUG)
	if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
		bufferspeciesname(0, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_GetGiftMonPartySlot)
			call(Common_EventScript_NameReceivedPartyMon)
		}
	} else {
		bufferspeciesname(0, VAR_TEMP_0)
		copyvar(VAR_TEMP_TRANSFERRED_SPECIES, VAR_TEMP_0)
		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(Common_EventScript_NameReceivedBoxMon)
		}
		call(Common_EventScript_TransferredToPC)
	}
	setflag(FLAG_ZONE2A_NUZLOCKE_BOUGHT_BUG)
	msgbox(format("Pleasure doing business with you."))
	release
	end
}

script Zone2A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE2A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE2A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone2A_EventScript_Random2 {
	lock
	if (flag(FLAG_ITEM_ZONE2A_RANDOM_2)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE2A_RANDOM_2)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone2A_EventScript_SilkScarf {
	lock
	if (flag(FLAG_ITEM_ZONE2A_SILK_SCARF)) {
	msgbox(gText_ItemChestIsEmpty)
	release
	end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE2A_SILK_SCARF)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_SILK_SCARF)
	release
	end
}

script Zone2A_EventScript_TMBugBite {
	lock
	if (flag(FLAG_ITEM_ZONE2A_TM_BUG_BITE)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_TM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE2A_TM_BUG_BITE)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_TM_BUG_BITE)
	release
	end
}

script Zone2A_EventScript_Polly {
	trainerbattle_single(TRAINER_PBL_POLLY, format("Our friend Shaun is our leader!\pYou'll have to defeat me to get to him!"), format("Defeated!"))
	msgbox(format("Shaun likes to use Pokémon that enjoy Oran Berries."), MSGBOX_AUTOCLOSE)
	end
}

script Zone2A_EventScript_Jeremiah {
	trainerbattle_single(TRAINER_PBL_JEREMIAH, format("The more legs, the better!"), format("Oh… Come on!"))
	msgbox(format("Just wait till my Bugs are all evolved!"), MSGBOX_AUTOCLOSE)
	end
}

script Zone2A_EventScript_Harper {
	trainerbattle_single(TRAINER_PBL_HARPER, format("Surprise! Battle time!"), format("My spirit, crushed!"))
	msgbox(format("I don't know if I can ever come back from this loss…"), MSGBOX_AUTOCLOSE)
	end
}

script Zone2A_EventScript_Josh {
	trainerbattle_single(TRAINER_PBL_JOSH, format("Hey, you! Get out of my way!"), format("You're too strong!"))
	msgbox(format("Sorry about earlier."), MSGBOX_AUTOCLOSE)
	end
}
