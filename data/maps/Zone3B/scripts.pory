mapscripts Zone3B_MapScripts {}

script Zone3B_EventScript_Phillipa {
    lock
	setspeaker("Phillipa")
	msgbox(format("This food is so tasty!"))
    release
    end
}

script Zone3B_EventScript_Remi {
    lock
	setspeaker("Remi")
	msgbox(format("I'm glad you like the food, kiddo.\pI wonder when your brother will get hungry; he seems upset about his Pokémon again."))
    release
    end
}

script Zone3B_EventScript_TradeZigzagoon {
	lock
	faceplayer
	if (flag(FLAG_ZONE3B_TRADED_ZIGZAGOON)) {
		msgbox(format("Please be good to my Zaggers!"))
		release
		end
	}
	setvar(VAR_0x8008, INGAME_TRADE_ZIGZAGOON)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Hey! Are you a {STR_VAR_2} fan?\pIf so, let's trade {STR_VAR_2}s!\pI'll trade you a Kantonian {STR_VAR_1} for my Galarian {STR_VAR_1}!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("Aww, you're a goon!"), MSGBOX_DEFAULT)
		release
		end
	}
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		msgbox(format("Aww, you're a goon!"), MSGBOX_DEFAULT)
		release
		end
	}
	specialvar(VAR_RESULT, IsPokemonFainted)
	if (var(VAR_RESULT) == TRUE) {
		msgbox(format("Hey! Don't trade me a fainted Pokémon!"))
		release
		end
	}
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	if (var(VAR_RESULT) != VAR_0x8009) {
		bufferspeciesname(STR_VAR_1, VAR_0x8009)
		msgbox(format("Huh? That doesn't look anything like a {STR_VAR_1} to me."), MSGBOX_DEFAULT)
		release
		end
	}
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(format("Please be good to my Zaggers!"), MSGBOX_DEFAULT)
	setflag(FLAG_ZONE3B_TRADED_ZIGZAGOON)
	release
	end
}
