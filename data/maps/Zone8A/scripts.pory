mapscripts Zone8A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE8A_MAGNET)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE8A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE8A_RANDOM_2)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone8A_EventScript_BuyTyrogue {
	lock
	faceplayer
	if (flag(FLAG_ZONE8A_BOUGHT_TYROGUE) == true) {
		msgbox(format("ALRIGHT! BACK TO TRAINING!"))
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("I'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pI'M TRAINING!\pErr… Oh!\pYou look like a capable Trainer!\pAre you interested in raising this Tyrogue?\pI won't part with it without ¥1000!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		hidemoneybox
		msgbox(format("ALRIGHT! BACK TO TRAINING!"))
		release
		end
	}
	checkmoney(1000, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox("You don't have enough money.\pALRIGHT! BACK TO TRAINING!")
		hidemoneybox
		release
		end
	}
	if (flag(FLAG_SETTINGS_NUZLOCKE) == true) {
		setvar(VAR_TEMP_3, 0) // counter for species clause
		//checkspeciesclause(SPECIES_TYROGUE)
		if (var(VAR_RESULT) == TRUE) {
			addvar(VAR_TEMP_3, 1)
		}
		if (var(VAR_TEMP_3) == 1) {
			msgbox(format("Unfortunately, you have already obtained a Pokémon in this evolution line."))
			hidemoneybox
			release
			end
		}
	}
	setvar(VAR_TEMP_1, SPECIES_TYROGUE)
	specialvar(VAR_TEMP_2, CalculatePlayerPartyCount)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		msgbox(format("Your party is full. Choose a Pokémon to send to the PC."), MSGBOX_AUTOCLOSE)
		hidemoneybox
		special(ChoosePartyMon)
		waitstate
		faceplayer
		showmoneybox(0, 0, 0)
		if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
			hidemoneybox
			msgbox(format("ALRIGHT! BACK TO TRAINING!"))
			release
			end
		}
		specialvar(VAR_TEMP_3, ScriptGetPartyMonSpecies)
		bufferspeciesname(2, VAR_TEMP_3)
		copyvar(VAR_TEMP_3, VAR_0x8004) // used in SendChosenMonToPC
		msgbox(format("Are you sure you want to send your {STR_VAR_3} to the PC?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("ALRIGHT! BACK TO TRAINING!"))
			release
			end
		}
	}
	msgbox(format("Just to confirm…\pYou're sure you want to buy Tyrogue for ¥1000?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		hidemoneybox
		msgbox(format("ALRIGHT! BACK TO TRAINING!"))
		release
		end
	}
	setflag(FLAG_ZONE8A_BOUGHT_TYROGUE)
	if (var(VAR_TEMP_2) == PARTY_SIZE) {
		special(SendChosenMonToPC)
		setvar(VAR_TEMP_2, 5) // gift mon appears always in last slot if party initially full
	}
	removemoney(1000)
	updatemoneybox
	msgbox(format("GOOD PURCHASE!"), MSGBOX_DEFAULT)
	hidemoneybox
	givemon(VAR_TEMP_1, 15, ITEM_NONE)
	setmonmetlocation(VAR_TEMP_2, 243)
	bufferspeciesname(0, VAR_TEMP_1)
	msgbox("Here's your {STR_VAR_1}.", MSGBOX_DEFAULT)
	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the {STR_VAR_1}!"))
	waitmessage
	waitfanfare
	msgbox(format("Would you like to give a nickname to {STR_VAR_1}?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		call(Common_EventScript_GetGiftMonPartySlot)
		call(Common_EventScript_NameReceivedPartyMon)
	}
	msgbox(format("TRAIN TYROGUE WELL!"), MSGBOX_DEFAULT)
	release
	end
}

script Zone8A_EventScript_Magnet {
	lock
	if (flag(FLAG_ITEM_ZONE8A_MAGNET)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE8A_MAGNET)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_MAGNET, 1)
	release
	end
}

script Zone8A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE8A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE8A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone8A_EventScript_Random2 {
	lock
	if (flag(FLAG_ITEM_ZONE8A_RANDOM_2)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE8A_RANDOM_2)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

raw `
Zone8A_EventScript_Jimmy::
	trainerbattle_single TRAINER_PBL_JIMMY, Zone8A_Text_JimmyIntro, Zone8A_Text_JimmyDefeat
	msgbox Zone8A_Text_JimmyPostBattle, MSGBOX_AUTOCLOSE
	end

Zone8A_Text_JimmyIntro:
	.string "Rare Pokémon… Do you have them?$"

Zone8A_Text_JimmyDefeat:
	.string "You do! You do have rare Pokémon!$"

Zone8A_Text_JimmyPostBattle:
	.string "I want to collect all the rare\n"
	.string "Pokémon in the world!$"

Zone8A_EventScript_Beckham::
	trainerbattle_single TRAINER_PBL_BECKHAM, Zone8A_Text_BeckhamIntro, Zone8A_Text_BeckhamDefeat
	msgbox Zone8A_Text_BeckhamPostBattle, MSGBOX_AUTOCLOSE
	end

Zone8A_Text_BeckhamIntro:
	.string "What's your favorite monster movie?$"

Zone8A_Text_BeckhamDefeat:
	.string "I like the ones with lots of\n"
	.string "destruction: all of them!$"

Zone8A_Text_BeckhamPostBattle:
	.string "Which one is my favorite?\n"
	.string "That's a good question…\l"
	.string "I'll have to think about it!$"

Zone8A_EventScript_Emiliano::
	trainerbattle_single TRAINER_PBL_EMILIANO, Zone8A_Text_EmilianoIntro, Zone8A_Text_EmilianoDefeat
	msgbox Zone8A_Text_EmilianoPostBattle, MSGBOX_AUTOCLOSE
	end

Zone8A_Text_EmilianoIntro:
	.string "I'm so hungry…$"

Zone8A_Text_EmilianoDefeat:
	.string "It's because I keep thinking about\n"
	.string "pancakes!$"

Zone8A_Text_EmilianoPostBattle:
	.string "Mmm… Pancakes…$"
`
