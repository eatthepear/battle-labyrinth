mapscripts Zone10Sanctuary_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ZONE, 10: Zone10Sanctuary_EventScript_Intro
	]
}

script Zone10Sanctuary_EventScript_Intro {
    lockall
	call(EventScript_EnterSanctuary)
	buffernumberstring(0, VAR_ZONE)
	msgbox(gText_Colress_ReachedZone, MSGBOX_DEFAULT)
	waitfanfare
	buffernumberstring(0, VAR_ZONE)
	msgbox(gText_RewardClear, MSGBOX_DEFAULT)
	msgbox(Zone10Sanctuary_Text_Reward, MSGBOX_DEFAULT)
	giveitem(ITEM_TERA_ORB)
	setflag(FLAG_USED_TERA)
	if (flag(FLAG_SETTINGS_INFINITE)) {
		msgbox("Because you're playing on Infinite, you also get Blue Shards!", MSGBOX_DEFAULT)
		giveitem(ITEM_BLUE_SHARD, 900)
	}
	msgbox(format("In battle, you can Terastilize your Pokémon now.\pPress {START_BUTTON} to Terastilize!\pAfter you use it, you'll have to go back to the Pokémon Center to recharge it."))
	msgbox(gText_Colress_Upgraded, MSGBOX_DEFAULT)
	playfanfare(MUS_OBTAIN_ITEM)
	msgbox(gText_Colress_UpgradedServices, MSGBOX_DEFAULT)
	waitfanfare
	setvar(VAR_ZONE, 11)
	buffernumberstring(0, VAR_ZONE)
	msgbox(gText_Colress_WhenUReady, MSGBOX_AUTOCLOSE)
	applymovement(1, Common_Movement_Colress_WalkUp)
	waitmovement(0)
	releaseall
	end
}

script Zone10Sanctuary_EventScript_NextZone {
	lock
	faceplayer
	message(gText_Colress_Greetings)
	waitmessage
	dynmultichoice(0, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Next Zone", "Settings", "Documentation", "Exit")
	switch (var(VAR_RESULT)) {
		case 0:
			buffernumberstring(0, VAR_ZONE)
			msgbox(gText_Colress_SureReadyToLeave, MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES) {
				call(EventScript_LeavingSetup)
				warp(MAP_ZONE11A, 255, 46, 2)
				waitstate
			}
		case 1:
			goto(EventScript_Settings)
		case 2:
			goto(EventScript_Documentation)
	}
	goto(EventScript_WhenUReady)
}

text Zone10Sanctuary_Text_Reward {
	format("The Tera Orb!")
}
