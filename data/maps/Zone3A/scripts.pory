mapscripts Zone3A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE3A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE3A_RANDOM_2)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE3A_RANDOM_3)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE3A_HYPER_POTION)) {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone3A_EventScript_TriggerLeviathan {
	lockall
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox("This looks like someone's home!\nGo inside?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		closemessage
		applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		releaseall
		end
	}
	closemessage
	hidefollower
	applymovement(LOCALID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	fadeoutbgm(1)
	applymovement(LOCALID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
	waitmovement(0)
	addobject(LOCALID_PHILLIPA)
	clearflag(FLAG_HIDE_ZONE3A_PHILLIPA)
	applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
	applymovement(LOCALID_PHILLIPA, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: You! How dare you intrude on my home!\pI am Phillipa, the all powerful!"))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_PHILLIPA_LEVIATHAN_3, format("Wah!!!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Wah!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PHILLIPA, Zone3A_Movement_Phillipa_RunAway)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Phillipa? Are you ok?"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	addobject(LOCALID_REMI)
	clearflag(FLAG_HIDE_ZONE3A_REMI)
	applymovement(LOCALID_REMI, Common_Movement_WalkDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Oh!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(32)
	applymovement(LOCALID_REMI, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: I see.\pYou must have defeated my daughter in battle.\pDon't worry, I'll go talk to her."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkToPhillipa)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Hey, kiddo."))
	applymovement(LOCALID_PHILLIPA, Common_Movement_FaceDown)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Wah! Daddy! I lost!"))
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: I know, love, but sometimes we lose, and that's life. We have to face our losses with honor, right?"), MSGBOX_AUTOCLOSE)
	delay(32)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Sniff…\pOkay…"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkBack)
	applymovement(LOCALID_PHILLIPA, Zone3A_Movement_Phillipa_WalkBack)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: What do you say?"))
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Sniff… Thank you very much for the battle!"))
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: I'm proud of you, kiddo."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkToPlayer)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Thanks for being a good sport.\pHere, I want you to have these."))
	giveitem(ITEM_LURE_BALL, 5)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkBackToPhillipa)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Remi{COLOR DARK_GRAY}: Alright, that's enough excitement for one day.\pIt's dinner time."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PHILLIPA, Common_Movement_FaceRight)
	waitmovement(0)
	msgbox(format("{COLOR BLUE}Phillipa{COLOR DARK_GRAY}: Food!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PHILLIPA, Common_Movement_WalkUp)
	waitmovement(0)
	setflag(FLAG_HIDE_ZONE3A_PHILLIPA)
	removeobject(LOCALID_PHILLIPA)
	applymovement(LOCALID_REMI, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(LOCALID_REMI, Common_Movement_WalkUp)
	waitmovement(0)
	setflag(FLAG_HIDE_ZONE3A_REMI)
	removeobject(LOCALID_REMI)
	setflag(FLAG_BEAT_LEVIATHAN_3)
	setvar(VAR_ZONE_3_STATE, 1)
	releaseall
	end
}

movement Zone3A_Movement_Phillipa_RunAway {
	walk_fast_left * 3
	walk_fast_up * 3
}

movement Zone3A_Movement_Remi_WalkToPhillipa {
	walk_left * 3
	walk_up * 2
}

movement Zone3A_Movement_Remi_WalkBack {
	walk_down * 2
	walk_right * 4
	face_down
}

movement Zone3A_Movement_Phillipa_WalkBack {
	walk_down * 3
	walk_right * 3
	face_down
}

movement Zone3A_Movement_Remi_WalkToPlayer {
	walk_down
	face_left
}

movement Zone3A_Movement_Remi_WalkBackToPhillipa {
	walk_up
	face_left
}

script Zone3A_EventScript_BugCatcher {
	lock
	faceplayer
	if (flag(FLAG_ZONE3A_RECEIVED_SILVER_POWDER) == true) {
		msgbox(format("Type-boosting items like Silver Powder boost the power of the corresponding type moves by 20%!"))
		release
		end
	}
	setflag(FLAG_ZONE3A_RECEIVED_SILVER_POWDER)
	msgbox(format("Bug-type Pokémon love Silver Powder.\pHere, take one!"))
	giveitem(ITEM_SILVER_POWDER)
	release
	end
}

script Zone3A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE3A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone3A_EventScript_Random2 {
	lock
	if (flag(FLAG_ITEM_ZONE3A_RANDOM_2)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_RANDOM_2)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone3A_EventScript_Random3 {
	lock
	if (flag(FLAG_ITEM_ZONE3A_RANDOM_3)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_RANDOM_3)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone3A_EventScript_HyperPotion {
	lock
	if (flag(FLAG_ITEM_ZONE3A_HYPER_POTION)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_HYPER_POTION)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_HYPER_POTION, 1)
	release
	end
}

script Zone3A_EventScript_Tutor {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	if (flag(FLAG_ZONE3A_MOVE_TUTOR_TAUGHT) == TRUE) {
		msgbox(format("Ho there, trainer!\pI can teach one of your Pokémon Water Pulse for the low price of ¥200. Interested?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Good day to you!"))
			release
			end
		}
	} else {
		msgbox(format("Ho there, trainer!\pI can teach one of your Pokémon Water Pulse. First time is free, while subsequent times cost ¥200! Interested?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == NO) {
			hidemoneybox
			msgbox(format("Good day to you!"))
			release
			end
		}
	}
	if (flag(FLAG_ZONE3A_MOVE_TUTOR_TAUGHT) == TRUE) {
		checkmoney(200, 0)
		if (var(VAR_RESULT) == FALSE) {
			msgbox(gText_YouDontHaveEnoughMoney)
			hidemoneybox
			release
			end
		}
	}
	setvar(VAR_TEMP_0, MOVE_WATER_PULSE)
	setvar(VAR_0x8005, MOVE_WATER_PULSE)
	buffermovename(0, VAR_TEMP_0)
	msgbox("Which Pokémon should learn\n{STR_VAR_1}?", MSGBOX_DEFAULT)
	special(ChooseMonForMoveTutor)
	waitstate
	lock
	faceplayer
	if (var(VAR_RESULT) == 0) {
		msgbox(format("Good day to you!"))
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("There! It's done. Use that move well!"), MSGBOX_DEFAULT)
	if (flag(FLAG_ZONE3A_MOVE_TUTOR_TAUGHT) == FALSE) {
		setflag(FLAG_ZONE3A_MOVE_TUTOR_TAUGHT)
	} else {
		removemoney(200)
		updatemoneybox
		msgbox(format("{PLAYER} paid ¥200."), MSGBOX_DEFAULT)
		msgbox(format("Good day to you!"))
	}
	hidemoneybox
	release
	end
}

script Zone3A_EventScript_Patrick {
	trainerbattle_single(TRAINER_PBL_PATRICK, format("My friends call me the free Exp guy, but I'll prove them wrong!"), format("Oops, guess not…"))
	msgbox(format("I guess I am the free Exp guy…"), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Jemma {
	trainerbattle_single(TRAINER_PBL_JEMMA, format("Hey! You have to pay to cross!"), format("Uh… You can go for free!"))
	msgbox(format("I was scamming people so well too…"), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Annabelle {
	trainerbattle_single(TRAINER_PBL_ANNABELLE, format("I wonder what day it is…"), format("Apparently it's defeat day!"))
	msgbox(format("Tomorrow will be another day."), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Boris {
	trainerbattle_single(TRAINER_PBL_BORIS, format("Never give up, never surrender!"), format("I surrender!"))
	msgbox(format("I'm a hypocrite, I know."), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Richard {
	trainerbattle_single(TRAINER_PBL_RICHARD, format("I have far too much money to deal with the likes of you!"), format("Agh! My money!"))
	msgbox(format("Do you think I can buy humility with my money?"), MSGBOX_AUTOCLOSE)
	end
}
