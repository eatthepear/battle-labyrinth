mapscripts Zone3A_MapScripts {
	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_ITEM_ZONE3A_RANDOM_1)) {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE3A_RANDOM_2)) {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE3A_RANDOM_3)) {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST)
		}
		if (flag(FLAG_ITEM_ZONE3A_HYPER_POTION)) {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
		} else {
			setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST)
		}
	}
}

script Zone3A_EventScript_TriggerLeviathan {
	lockall
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	msgbox("This looks like someone's home!\nGo inside?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		closemessage
		applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
		waitmovement(0)
		releaseall
		end
	}
	closemessage
	hidefollower
	applymovement(LOCALID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	fadeoutbgm(1)
	applymovement(LOCALID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
	waitmovement(0)
	addobject(LOCALID_PHILLIPA)
	clearflag(FLAG_HIDE_ZONE3A_PHILLIPA)
	applymovement(LOCALID_PLAYER, Common_Movement_WalkDown)
	applymovement(LOCALID_PHILLIPA, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	setspeaker("Phillipa")
	msgbox(format("You! How dare you intrude on my home!\pI am Phillipa, the all powerful!"))
	setflag(FLAG_FORCE_BATTLE_ANIM_ON)
	trainerbattle_no_intro(TRAINER_PBL_PHILLIPA_LEVIATHAN_3, format("Wah!!!"))
	clearflag(FLAG_FORCE_BATTLE_ANIM_ON)
	setspeaker("Phillipa")
	msgbox(format("Wah!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PHILLIPA, Zone3A_Movement_Phillipa_RunAway)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("Phillipa? Are you ok?"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	waitmovement(0)
	waitse
	addobject(LOCALID_REMI)
	clearflag(FLAG_HIDE_ZONE3A_REMI)
	applymovement(LOCALID_REMI, Common_Movement_WalkDown)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("Oh!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Common_Movement_FaceLeft)
	waitmovement(0)
	delay(32)
	applymovement(LOCALID_REMI, Common_Movement_FaceDown)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("I see.\pYou must have defeated my daughter in battle.\pDon't worry, I'll go talk to her."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkToPhillipa)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("Hey, kiddo."))
	applymovement(LOCALID_PHILLIPA, Common_Movement_FaceDown)
	waitmovement(0)
	setspeaker("Phillipa")
	msgbox(format("Wah! Daddy! I lost!"))
	setspeaker("Remi")
	msgbox(format("I know, love, but sometimes we lose, and that's life. We have to face our losses with honor, right?"), MSGBOX_AUTOCLOSE)
	delay(32)
	setspeaker("Phillipa")
	msgbox(format("Sniff…\pOkay…"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkBack)
	applymovement(LOCALID_PHILLIPA, Zone3A_Movement_Phillipa_WalkBack)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("What do you say?"))
	setspeaker("Phillipa")
	msgbox(format("Sniff… Thank you very much for the battle!"))
	setspeaker("Remi")
	msgbox(format("I'm proud of you, kiddo."), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkToPlayer)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("Thanks for being a good sport.\pHere, I want you to have these."))
	giveitem(ITEM_LURE_BALL, 5)
	closemessage
	applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	applymovement(LOCALID_REMI, Zone3A_Movement_Remi_WalkBackToPhillipa)
	waitmovement(0)
	setspeaker("Remi")
	msgbox(format("Alright, that's enough excitement for one day.\pIt's dinner time."))
	applymovement(LOCALID_PHILLIPA, Common_Movement_FaceRight)
	waitmovement(0)
	setspeaker("Phillipa")
	msgbox(format("Food!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_PHILLIPA, Common_Movement_WalkUp)
	waitmovement(0)
	setflag(FLAG_HIDE_ZONE3A_PHILLIPA)
	removeobject(LOCALID_PHILLIPA)
	applymovement(LOCALID_REMI, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(LOCALID_REMI, Common_Movement_WalkUp)
	waitmovement(0)
	setflag(FLAG_HIDE_ZONE3A_REMI)
	removeobject(LOCALID_REMI)
	setflag(FLAG_BEAT_LEVIATHAN_3)
	setvar(VAR_ZONE_3_STATE, 1)
	releaseall
	end
}

movement Zone3A_Movement_Phillipa_RunAway {
	walk_fast_left * 3
	walk_fast_up * 3
}

movement Zone3A_Movement_Remi_WalkToPhillipa {
	walk_left * 3
	walk_up * 2
}

movement Zone3A_Movement_Remi_WalkBack {
	walk_down * 2
	walk_right * 4
	face_down
}

movement Zone3A_Movement_Phillipa_WalkBack {
	walk_down * 3
	walk_right * 3
	face_down
}

movement Zone3A_Movement_Remi_WalkToPlayer {
	walk_down
	face_left
}

movement Zone3A_Movement_Remi_WalkBackToPhillipa {
	walk_up
	face_left
}

script Zone3A_EventScript_Trade {
	lock
	faceplayer
	if (flag(FLAG_ZONE3A_TRADE)) {
		msgbox(Zone3A_Text_BeGood)
		release
		end
	}
	setvar(VAR_0x8008, INGAME_TRADE_FARFETCHD)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(format("Hey! Are you a {STR_VAR_2} fan?\pIf so, let's trade {STR_VAR_2}s!\pI'll trade you a Galarian {STR_VAR_1} for a Kantonian {STR_VAR_1}!"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(Zone3A_Text_ComeBack)
		release
		end
	}
	special(ChoosePartyMon)
	waitstate
	copyvar(VAR_0x800A, VAR_0x8004)
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		msgbox(Zone3A_Text_ComeBack)
		release
		end
	}
	specialvar(VAR_RESULT, IsPokemonFainted)
	if (var(VAR_RESULT) == TRUE) {
		msgbox(format("Hey! Don't trade me a fainted Pokémon!"))
		release
		end
	}
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	if (var(VAR_RESULT) != VAR_0x8009) {
		bufferspeciesname(STR_VAR_1, VAR_0x8009)
		msgbox(format("Huh? That doesn't look anything like a {STR_VAR_1} to me."), MSGBOX_DEFAULT)
		release
		end
	}
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
	msgbox(Zone3A_Text_BeGood)
	setflag(FLAG_ZONE3A_TRADE)
	release
	end
}

text Zone3A_Text_ComeBack {
	format("Come back with a Kantonian Farfetch'd if you want to trade!")
}

text Zone3A_Text_BeGood {
	format("Please be good to my Farfetch'd!\pI wish I had a {COLOR RED}Notebook{COLOR DARK_GRAY} to see how to evolve a Galarian Farfetch'd…")
}

script Zone3A_EventScript_Random1 {
	lock
	if (flag(FLAG_ITEM_ZONE3A_RANDOM_1)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_RANDOM_1)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone3A_EventScript_Random2 {
	lock
	if (flag(FLAG_ITEM_ZONE3A_RANDOM_2)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_RANDOM_2)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone3A_EventScript_Random3 {
	lock
	if (flag(FLAG_ITEM_ZONE3A_RANDOM_3)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_2, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_RANDOM_3)
	addobject(VAR_LAST_TALKED)
	specialvar(VAR_TEMP_1, RandomItemChest)
	finditem(VAR_TEMP_1, VAR_TEMP_0)
	release
	end
}

script Zone3A_EventScript_HyperPotion {
	lock
	if (flag(FLAG_ITEM_ZONE3A_HYPER_POTION)) {
		msgbox(gText_ItemChestIsEmpty)
		release
		end
	}
	waitse
	removeobject(VAR_LAST_TALKED)
	setvar(VAR_OBJ_GFX_ID_3, OBJ_EVENT_GFX_ITEM_CHEST_OPEN)
	setflag(FLAG_ITEM_ZONE3A_HYPER_POTION)
	addobject(VAR_LAST_TALKED)
	finditem(ITEM_HYPER_POTION, 1)
	release
	end
}

script Zone3A_EventScript_Tutor {
	lock
	faceplayer
	showmoneybox(0, 0, 0)
	msgbox(format("Ahoy there, trainer!\pI can teach your Pokémon some watery techniques, at the low price of ¥400!"))
	message(format("What move should I teach?\nThey will cost you ¥400."))
	waitmessage
	dynmultipush("Water Pulse", MOVE_WATER_PULSE)
	dynmultipush("Chilling Water", MOVE_CHILLING_WATER)
	dynmultipush("Exit", 1)
	dynmultistack(21, 0, FALSE, 5, 0, 0, DYN_MULTICHOICE_CB_NONE)
	switch (var(VAR_RESULT)) {
		case MOVE_WATER_PULSE:
			setvar(VAR_TEMP_0, MOVE_WATER_PULSE)
			setvar(VAR_0x8005, MOVE_WATER_PULSE)
		case MOVE_CHILLING_WATER:
			setvar(VAR_TEMP_0, MOVE_CHILLING_WATER)
			setvar(VAR_0x8005, MOVE_CHILLING_WATER)
		case 1:
		case MULTI_B_PRESSED:
			hidemoneybox
			msgbox(Zone3A_Text_GoodDay)
			release
			end
	}
	checkmoney(400, 0)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	buffermovename(0, VAR_TEMP_0)
	msgbox("Which Pokémon should learn\n{STR_VAR_1}?")
	special(ChooseMonForMoveTutor)
	waitstate
	lock
	faceplayer
	if (var(VAR_RESULT) == 0) {
		msgbox(Zone3A_Text_GoodDay)
		release
		end
	}
	showmoneybox(0, 0, 0)
	msgbox(format("There! It's done. Use that move well!"))
	removemoney(400)
	updatemoneybox
	msgbox(format("{PLAYER} paid ¥400."))
	hidemoneybox
	msgbox(Zone3A_Text_GoodDay)
	release
	end
}

text Zone3A_Text_GoodDay {
	format("Good day to you!")
}

script Zone3A_EventScript_Patrick {
	trainerbattle_single(TRAINER_PBL_PATRICK, format("I wonder what day it is…"), format("Apparently it's defeat day!"))
	msgbox(format("Tomorrow will be another day."), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Jemma {
	trainerbattle_single(TRAINER_PBL_JEMMA, format("Hey! You have to pay to cross!"), format("Uh… You can go for free!"))
	msgbox(format("I was scamming people so well too…"), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Boris {
	trainerbattle_single(TRAINER_PBL_BORIS, format("Never give up, never surrender!"), format("I surrender!"))
	msgbox(format("I'm a hypocrite, I know."), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_Richard {
	trainerbattle_single(TRAINER_PBL_RICHARD, format("My friends call me the free Exp guy, but I'll prove them wrong!"), format("Oops, guess not…"))
	msgbox(format("I guess I am the free Exp guy…"), MSGBOX_AUTOCLOSE)
	end
}

script Zone3A_EventScript_LunaAndNora {
	trainerbattle_double(TRAINER_PBL_LUNA_AND_NORA, format("Up ahead is a strong lady!\pShe's guarding something special!"), format("We hear she's so strong, she's written about in the Notebook!"), gText_NotEnoughMons)
	msgbox(format("You don't have to battle her if you don't want to!"), MSGBOX_DEFAULT)
	release
	end
}

script Zone3A_EventScript_Melinda {
	trainerbattle_single(TRAINER_PBL_MELINDA_OPTIONAL_1, format("You want this Rare Candy? You won't get it!\pI'll defeat you with the power of the Pokémon my partner gave me!"), format("Fine… take the Rare Candy! It's really rare, so be careful how you use it!"))
	msgbox(format("Rare Candies are typically guarded by an optional miniboss, but can be used to increase your Pokémon's level over the level cap."), MSGBOX_AUTOCLOSE)
	end
}
