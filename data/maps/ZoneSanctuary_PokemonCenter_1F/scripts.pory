mapscripts ZoneSanctuary_PokemonCenter_1F_MapScripts {

}

script EventScript_Joy {
	lock
	faceplayer
	if (flag(FLAG_JOY_INTRO) == false) {
		setflag(FLAG_JOY_INTRO)
		msgbox(format("{COLOR BLUE}???{COLOR DARK_GRAY}: Welcome to Joy's Center of Restoration! My name is Joy.\p{COLOR BLUE}Joy{COLOR DARK_GRAY}: I used to work at the Pokémon Center, but they gave terrible pay.\pSo now, this building, Joy's Center of Restoration, is my own! I own it! Isn't that amazing?\pAnyways, not only do we encourage folks to use the building as a social space, we also still heal your Pokémon and offer access to the PC, for free!"), MSGBOX_DEFAULT)
	}
	msgbox(format("{COLOR BLUE}Joy{COLOR DARK_GRAY}: Welcome! Should I heal your Pokémon?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		closemessage
		incrementgamestat(GAME_STAT_USED_POKECENTER)
		hidefollower(0)
		fadescreenswapbuffers(FADE_TO_BLACK)
		playfanfare(MUS_HEAL)
		waitfanfare
		special(HealPlayerParty)
		fadescreenswapbuffers(FADE_FROM_BLACK)
		callnative(UpdateFollowingPokemon)
		applymovement(LOCALID_JOY, Movement_PkmnCenterNurse_Bow)
		waitmovement(0)
		msgbox(format("{COLOR BLUE}Joy{COLOR DARK_GRAY}: Your Pokémon have been restored. See you next time!"))
	}
	release
	end
}

script EventScript_Chansey {
	lock
	faceplayer
	playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)
	msgbox(format("Chan!"))
	waitmoncry
	release
	end
}

script EventScript_SignsQuestGirl {
	lock
	faceplayer
	questmenu(QUEST_MENU_CHECK_UNLOCKED, QUEST_SIGNS)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(format("Hiya! I'm improving on my reading skills!\pYou should work on your reading skills too!\pIf you read all the signs in the Pokémon Battle Labyrinth, I'll give you a nice reward!"))
		startquest(QUEST_SIGNS)
	}
	msgbox(format("Signs have lots of useful info about unique mechanics to the Pokémon Battle Labyrinth!\pYou can improve your reading skills while learning more about the challenges at hand!"))
	release
	end
}

script EventScript_SavesQuestBoy {
	lock
	faceplayer
	questmenu(QUEST_MENU_CHECK_UNLOCKED, QUEST_SAVES)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(format("Saving your progress is important while playing games.\pI learned that the hard way!\pDon't be like me!\pI'll give you something if you can save the game 50 times!"))
		startquest(QUEST_SAVES)
	}
	questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_SAVES)
	if (var(VAR_RESULT) == FALSE) {
		callnative(SavesQuestCheckIfDone)
		if (flag(FLAG_TEMP_6) == TRUE) {
			msgbox(format("Wow!\pYou really went and saved 50 times?\pThat's crazy.\pHere, as a reward, I can give you this nifty Exp. Share!\pI would only use it if I don't want my Pokémon to grow at the rate it's supposed to!"))
			giveitem(ITEM_EXP_SHARE)
			completequest(QUEST_SAVES)
		}
	}
	msgbox(format("Don't forget to save your game!"))
	release
	end
}

script EventScript_PokedexQuestScientist {
	lock
	faceplayer
	questmenu(QUEST_MENU_CHECK_UNLOCKED, QUEST_POKEDEX)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(format("Kids these days are always neglecting filling their Pokédex!\pIf you can show me a Pokédex that's seen all the Pokémon in existence, I'll reward you!"))
		startquest(QUEST_POKEDEX)
	}
	msgbox(format("Seeing all the Pokémon is tough enough.\pImagine capturing them all!"))
	release
	end
}
