mapscripts ZoneSanctuary_PokemonCenter_1F_MapScripts {

}

script EventScript_Joy {
	lock
	faceplayer
	if (flag(FLAG_JOY_INTRO) == false) {
		setflag(FLAG_JOY_INTRO)
		setspeaker("???")
		msgbox(format("Welcome to Joy's Center of Restoration! My name is Joy."))
		setspeaker("Joy")
		msgbox(format("I used to work at the Pokémon Center, but they gave terrible pay.\pSo now, this building, Joy's Center of Restoration, is my own! I own it! Isn't that amazing?\pAnyways, not only do we encourage folks to use the building as a social space, we also still heal your Pokémon and offer access to the PC, for free!"), MSGBOX_DEFAULT)
	}
	setspeaker("Joy")
	msgbox(format("Welcome! Should I heal your Pokémon?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		closemessage
		incrementgamestat(GAME_STAT_USED_POKECENTER)
		hidefollower(0)
		fadescreenswapbuffers(FADE_TO_BLACK)
		playfanfare(MUS_HEAL)
		waitfanfare
		special(HealPlayerParty)
		fadescreenswapbuffers(FADE_FROM_BLACK)
		callnative(UpdateFollowingPokemon)
		applymovement(LOCALID_JOY, Movement_PkmnCenterNurse_Bow)
		waitmovement(0)
		setspeaker("Joy")
		msgbox(format("Your Pokémon have been restored. See you next time!"), MSGBOX_AUTOCLOSE)
		applymovement(LOCALID_PLAYER, Common_Movement_FaceDown)
		waitmovement(0)
	}
	release
	end
}

script EventScript_AfterWhiteOutHeal_PBL {
	lockall
	if (flag(FLAG_SETTINGS_NUZLOCKE)) {
		msgbox(format("You have wiped. {COLOR RED}Nuzlocke{COLOR DARK_GRAY} mode turned off."))
		clearflag(FLAG_SETTINGS_NUZLOCKE)
	}
	setspeaker("Joy")
	msgbox(format("First, let's restore your Pokémon to full health."))
	call(EventScript_PkmnCenterNurse_TakeAndHealPkmn)
	setspeaker("Joy")
	msgbox(format("Your Pokémon have been healed to perfect health.\pYou can always speak to Colress to decrease the difficulty.\pWe hope you excel!"))
	applymovement(VAR_LAST_TALKED, Movement_PkmnCenterNurse_Bow)
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement(0)
	fadedefaultbgm
	releaseall
	end
}

script EventScript_Chansey {
	lock
	faceplayer
	setspeaker("Chansey")
	playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)
	msgbox(format("Chan!"))
	waitmoncry
	release
	end
}

script EventScript_SignsQuestGirl {
	lock
	faceplayer
	questmenu(QUEST_MENU_CHECK_UNLOCKED, QUEST_SIGNS)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(format("Hiya! I'm improving on my reading skills!\pYou should work on your reading skills too!\pIf you read all the signs in Pokémon Battle Labyrinth, I'll give you a nice reward!"))
		startquest(QUEST_SIGNS)
		msgbox(format("You can see the signs you've already read in the Quest Menu."))
	}
	msgbox(format("Signs have lots of useful info about unique mechanics to Pokémon Battle Labyrinth!\pAll signs get their messages transcribed to the Notebook after you read them.\pThe Notebook also has lots of useful information independent of signs, it's worth a read!"))
	release
	end
}

script EventScript_SavesQuestBoy {
	lock
	faceplayer
	questmenu(QUEST_MENU_CHECK_UNLOCKED, QUEST_SAVES)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(format("Saving your progress is important while playing games.\pI learned that the hard way!\pDon't be like me!\pI'll give you something if you can save the game 50 times!"))
		startquest(QUEST_SAVES)
	}
	questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_SAVES)
	if (var(VAR_RESULT) == FALSE) {
		callnative(SavesQuestCheckIfDone)
		if (flag(FLAG_TEMP_6) == TRUE) {
			msgbox(format("Wow!\pYou really went and saved 50 times?\pThat's crazy.\pHere, as a reward, I can give you this nifty Exp. Share!\pI would only use it if I don't want my Pokémon to grow at the rate it's supposed to!"))
			giveitem(ITEM_EXP_SHARE)
			completequest(QUEST_SAVES)
		}
	}
	msgbox(format("Don't forget to save your game!"))
	release
	end
}

script EventScript_PokedexQuestScientist {
	lock
	faceplayer
	questmenu(QUEST_MENU_CHECK_UNLOCKED, QUEST_POKEDEX)
	if (var(VAR_RESULT) == FALSE) {
		msgbox(format("Kids these days are always neglecting filling their Pokédex!\pIf you can show me a Pokédex that's seen all the Pokémon in existence, I'll reward you!"))
		startquest(QUEST_POKEDEX)
	}
	msgbox(format("Seeing all the Pokémon is tough enough.\pImagine capturing them all!"))
	release
	end
}

script EventScript_OldMan_PriorZone {
	lock
	faceplayer
	if (var(VAR_ZONE) == 1) {
		msgbox(format("Looking to access a prior Zone?\pHm, it seems you have not completed any Zones yet. Come back when you have."))
		release
		end
	}
	showmoneybox(18, 0)
	message(format("Looking to access a prior Zone?\nIt will cost you ¥1000."))
	waitmessage
	setvar(VAR_TEMP_0, 1)
	while (var(VAR_TEMP_0) != VAR_ZONE) {
		call(EventScript_BufferZoneNameShort)
		dynmultipush("{STR_VAR_1}", VAR_TEMP_0)
		addvar(VAR_TEMP_0, 1)
	}
	dynmultipush("Cancel", 0)
	dynmultistack(0, 0, FALSE, 6, FALSE, 1, DYN_MULTICHOICE_CB_NONE)
	switch (var(VAR_RESULT)) {
		case 0:
		case MULTI_B_PRESSED:
			hidemoneybox
			release
			end
		default:
			copyvar(VAR_TEMP_0, VAR_RESULT)
	}
	call(EventScript_BufferZoneName)
	msgbox(format("Are you sure you want to go to {STR_VAR_1}?\lIt will cost you ¥1000."), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		hidemoneybox
		release
		end
	}
	if (checkmoney(1000) == FALSE) {
		msgbox(gText_YouDontHaveEnoughMoney)
		hidemoneybox
		release
		end
	}
	removemoney(1000)
	updatemoneybox
	msgbox(format("Thank you very much…"), MSGBOX_DEFAULT)
	setflag(FLAG_IS_REVISITING_ZONE)
	switch (var(VAR_TEMP_0)) {
		case 1:
			call(EventScript_OldMan_MoveOutOfWay)
			warpdoor(MAP_ZONE1A, 255, 16, 28)
			waitstate
		case 2:
			call(EventScript_OldMan_MoveOutOfWay)
			warpdoor(MAP_ZONE2A, 255, 10, 8)
			waitstate
		case 3:
			call(EventScript_OldMan_MoveOutOfWay)
			warpdoor(MAP_ZONE3A, 255, 38, 25)
			waitstate
		case 4:
			call(EventScript_OldMan_MoveOutOfWay)
			warpdoor(MAP_ZONE4A, 255, 3, 4)
			waitstate
	}
	release
	end
}

script EventScript_OldMan_MoveOutOfWay {
	closemessage
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 15) {
		applymovement(LOCALID_OLD_MAN, Common_Movement_WalkDown)
		applymovement(LOCALID_PLAYER, Common_Movement_WalkRight)
		waitmovement(0)
		applymovement(LOCALID_OLD_MAN, Common_Movement_FaceUp)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 16) {
		applymovement(LOCALID_OLD_MAN, Common_Movement_WalkLeft)
		applymovement(LOCALID_PLAYER, Common_Movement_WalkUp)
		waitmovement(0)
		applymovement(LOCALID_OLD_MAN, Common_Movement_FaceRight)
		waitmovement(0)
	} else {
		applymovement(LOCALID_OLD_MAN, Common_Movement_WalkDown)
		applymovement(LOCALID_PLAYER, Common_Movement_WalkLeft)
		waitmovement(0)
		applymovement(LOCALID_OLD_MAN, Common_Movement_FaceUp)
		waitmovement(0)
	}
	return
}
